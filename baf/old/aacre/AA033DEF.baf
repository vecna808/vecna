IF
	See(NearestEnemyOf(Myself))
	!Range(LastSeenBy(Myself),10)
	Global("Prep","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("Prep","LOCALS",1)
		SetGlobalTimer("NextSpell","LOCALS",TWO_MINUTES)
		SpellNoDec(Myself,ROGUE_ASSASINATION)
END

IF
	See(NearestEnemyOf(Myself))
	GlobalTimerExpired("NextSpell","LOCALS")
	!Range(LastSeenBy(Myself),10)
	Global("Prep","LOCALS",1)
THEN
	RESPONSE #100
		SetGlobal("Prep","LOCALS",2)
		SpellNoDec(Myself,ROGUE_EVASION)
END

IF
	See(NearestEnemyOf(Myself))
	!StateCheck(Myself,STATE_INVISIBLE)
	!StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
	!GlobalTimerNotExpired("ThiefHide","LOCALS")
THEN
	RESPONSE #100
		SetGlobalTimer("ThiefHide","LOCALS",ONE_ROUND)
		ApplySpell(Myself,WIZARD_INVISIBILITY)
END

IF
	Allegiance(Myself,ENEMY)
	Detect([PC])
	!See(LastSeenBy(Myself))
	Delay(3)
THEN
	RESPONSE #70
		DisplayStringHead(Myself,31858) // Detects Illusion
		ReallyForceSpell(LastSeenBy(Myself),THIEF_DETECT_ILLUSION)
	RESPONSE #30
		DisplayStringHead(Myself,31859) // Fails to detect illusions
END

IF
	HPPercentLT(Myself,50)
	!GlobalTimerNotExpired("DrankPotionTimer","LOCALS")
	HasItem("POTN55",Myself) // Potion of Superior Healing
THEN
	RESPONSE #100
		SetGlobalTimer("DrankPotionTimer","LOCALS",ONE_ROUND)
		DisplayStringHead(Myself,46150) // quaffs a potion
		UseItem("POTN55",Myself) // Potion of Superior Healing
END

IF
	See(NearestEnemyOf(Myself))
	OR(2)
		StateCheck(Myself,STATE_INVISIBLE)
		StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		MoveToObject(NearestEnemyOf(Myself))
		DisplayStringHead(Myself,72409) // Die!  For the Black Sun!!
		AttackReevaluate(NearestEnemyOf(Myself),30)
		RunAwayFrom(NearestEnemyOf(Myself),60)
END

IF
	See(NearestEnemyOf(Myself))
	!StateCheck(Myself,STATE_INVISIBLE)
	!StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
	Range(NearestEnemyOf(Myself),5)
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		AttackReevaluate(NearestEnemyOf(Myself),30)
		RunAwayFrom(NearestEnemyOf(Myself),60)
END

IF
	See(NearestEnemyOf(Myself))
	!StateCheck(Myself,STATE_INVISIBLE)
	!StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
	!Range(NearestEnemyOf(Myself),5)
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		RunAwayFrom(NearestEnemyOf(Myself),30)
END


