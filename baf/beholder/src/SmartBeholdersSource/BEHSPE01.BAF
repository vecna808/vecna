// **********************************************************************
// *               BAF Name: BEHSPE01.BAF
// *                Created: 10/01/2001 11:31:58 PM
// *       BAF Assembled by: AIScriptFactory Text
// **********************************************************************
// *               BAF Name: BEHSPE01.BAF
// *           Segment Name: 00 0 Header - BEHSPE01.baf
// Spectator
// Power                           HPGT    Wait
// BEHOLDER_CAUSE_SERIOUS_WOUNDS   20      3
// BEHOLDER_CHARM_PERSON           45      4
// BEHOLDER_FEAR                   65      1
// BEHOLDER_PARALYZATION           35      0
// BEHOLDER_SPELL_TURNING          45, 0   0, Delay 6
// Spots bouncing after 6 seconds.
// Spots immunities after 18 seconds.
//------------------------------------------------------------------------------
// Special case:
// Not necessarily hostile.
// Keep an eye on the party.
// Run when attacked.
//==============================================================================
// Keep staring at the nearest party member.
IF
    // I am still neutral.
    Allegiance(Myself,NEUTRAL)
    // The party is not allowed in the chest (yet).
    !Global("SpectatorAllowsChest","GLOBAL",1)
THEN
    RESPONSE #100
        FaceObject([GOODCUTOFF])
END
// Look the "other way".
IF
    // I am still neutral.
    Allegiance(Myself,NEUTRAL)
    // The party *is* allowed in the chest (yet).
    Global("SpectatorAllowsChest","GLOBAL",1)
THEN
    RESPONSE #100
        Face(1)
END
// Not necessarily hostile (just in case the above fails).
// (The Spectator's Override script (GENSHT01) will turn him hostile if attacked or damaged.)
IF
    Allegiance(Myself,NEUTRAL)
THEN
    RESPONSE #100
        NoAction()
END
// Run when attacked.
IF
    OR(2)
        HitBy([ANYTHING],DEFAULT)
        AttackedBy([ANYTHING],DEFAULT)
    Exists(LastAttackerOf())
THEN
    RESPONSE #100
        RunAwayFrom(LastAttackerOf(),6)
END
// *               BAF Name: BEHSPE01.BAF
// *           Segment Name: 01 1 Behavior - 3 Investigate.baf
    // Move around when something suspicious happens (such as off-screen Cloudkilling).
    IF
        // No-one to be SEEN.
        !See(Player1)
        !See(Player2)
        !See(Player3)
        !See(Player4)
        !See(Player5)
        !See(Player6)
        !See(NearestEnemyOf())
        OR(4)
            // They were here...
            Global("xyxInitialized","LOCALS",1)
            // or someone called for reinforcements...
            // (This Shout number is used in GENSHT01.BCS, the override script for all Beholders.)
            // (GENSHT.BCS moves the Beholder up to another Beholder, not necessarily up to the party.)
            Heard([ANYONE],89)
            // (This Shout is used in WTASIGHT.BCS and WTARSGT.BCS.)
            Heard([ANYONE],111)
            // or I am taking damage from an unseen source (such as off-screen Cloudkilling).
            HitBy([ANYTHING],DEFAULT)
        !StateCheck(Myself,STATE_CHARMED)
    THEN
        RESPONSE #100
            // Move around.
            RandomWalkContinuous()
    END
// *               BAF Name: BEHSPE01.BAF
// *           Segment Name: 02 1 Behavior - 4 Cut-Off - Stand Ground.baf
    // Performance saver.
    IF
        // Target Player1-6 in case any of them is Charmed.
        !See(Player1)
        !See(Player2)
        !See(Player3)
        !See(Player4)
        !See(Player5)
        !See(Player6)
        !See(NearestEnemyOf())
    THEN
        RESPONSE #100
            // Restart
    END
// *               BAF Name: BEHSPE01.BAF
// *           Segment Name: 03 1 Behavior - 5 Initialize - 1 Bounce.baf
    // Initialize.
    IF
        // This only runs if there are GOODCUTOFF visible.
        Global("xyxInitialized","LOCALS",0)
    THEN
        RESPONSE #100
            // Call for reinforcements  (Don't wait until attacked...).
            // (This Shout number is used in GENSHT01.BCS, the override script for all Beholders.)
            Shout(89)
            // Beholders are too smart to target a bouncing or immune character after they notice it.
            // Notice bouncing after one round.
            SetGlobalTimer("xyxDoesNotSeeBounce","LOCALS",6)
// *               BAF Name: BEHSPE01.BAF
// *           Segment Name: 04 1 Behavior - 5 Initialize - 2 Immunities.baf
            // Notice immunity after a couple of rounds.
            SetGlobalTimer("xyxDoesNotSeeImmunity","LOCALS",18)
            SetGlobal("xyxInitialized","LOCALS",1)
            Continue()
    END
//==============================================================================
// *               BAF Name: BEHSPE01.BAF
// *           Segment Name: 05 4 Innate - 4 Spell Turning.baf
    // Innate Spell Turning.
    IF
        // Not all the time.
        OR(2)
            Delay(6)
            HPGT(Myself,45)
        !HasBounceEffects(Myself)
        // There's a nasty spellcaster.
        OR(4)
            Exists([GOODCUTOFF.0.0.BARD])
            Exists([GOODCUTOFF.0.0.DRUID_ALL])
            Exists([GOODCUTOFF.0.0.CLERIC_ALL])
            Exists([GOODCUTOFF.0.0.MAGE_ALL])
    THEN
        RESPONSE #100
            DisplayString(Myself,10850) // Minor Spell Turning
            ApplySpell(Myself,BEHOLDER_SPELL_TURNING)
        RESPONSE #50
            Continue()
    END
// *               BAF Name: BEHSPE01.BAF
// *           Segment Name: 06 3 Targeting - Rays - Disabling.baf
// Find a target without Spell Turning, Magic Resistance or the Shield of Balduran.
// (The cut-off resets the LastSeenBy.)
// (Improved Invisibility is no problem for ForceSpell.)
    // Sixth Nearest.
    IF
        // Randomize a bit.
        !RandomNum(6,6)
        // Not bouncing ALL spells.
        OR(3)
            // The target isn't bouncing spells...
            !HasBounceEffects(SixthNearestEnemyOf())
            // or this Beholder hasn't caught on yet...
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            // or the target has the Cloak of Mirroring.
            HasItemEquiped("CLCK26",SixthNearestEnemyOf())
        OR(2)
            // This Beholder hasn't caught on yet...
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            // or the target doesn't have the Shield of Balduran.
            !HasItemEquiped("WA2SHIEL",SixthNearestEnemyOf())
        // Not already out of action.
        !StateCheck(SixthNearestEnemyOf(),STATE_OUT_OF_ACTION)
        // Not in Sanctuary.
        CheckStat(SixthNearestEnemyOf(),0,SANCTUARY)
        // Not too resistant.
        OR(2)
            // This Beholder hasn't noticed yet...
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            // or not too resistant.
            CheckStatLT(SixthNearestEnemyOf(),50,RESISTMAGIC)
        // Viconia is pretty resistant.  Beholders would know a Drow when they see one.
        !Name("Viconia",SixthNearestEnemyOf())
        // Not immortal.
        !HasItem("MINHP1",SixthNearestEnemyOf())
        !HasItem("IMOENHP1",SixthNearestEnemyOf())
        // Target.
        See(SixthNearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
    // Fifth Nearest.
    IF
        !RandomNum(6,5)
        OR(3)
            !HasBounceEffects(FifthNearestEnemyOf())
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            HasItemEquiped("CLCK26",FifthNearestEnemyOf())
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !HasItemEquiped("WA2SHIEL",FifthNearestEnemyOf())
        !StateCheck(FifthNearestEnemyOf(),STATE_OUT_OF_ACTION)
        CheckStat(FifthNearestEnemyOf(),0,SANCTUARY)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            CheckStatLT(FifthNearestEnemyOf(),50,RESISTMAGIC)
        !Name("Viconia",FifthNearestEnemyOf())
        !HasItem("MINHP1",FifthNearestEnemyOf())
        !HasItem("IMOENHP1",FifthNearestEnemyOf())
        See(FifthNearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
    // Fourth Nearest.
    IF
        !RandomNum(6,4)
        OR(3)
            !HasBounceEffects(FourthNearestEnemyOf())
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            HasItemEquiped("CLCK26",FourthNearestEnemyOf())
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !HasItemEquiped("WA2SHIEL",FourthNearestEnemyOf())
        !StateCheck(FourthNearestEnemyOf(),STATE_OUT_OF_ACTION)
        CheckStat(FourthNearestEnemyOf(),0,SANCTUARY)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            CheckStatLT(FourthNearestEnemyOf(),50,RESISTMAGIC)
        !Name("Viconia",FourthNearestEnemyOf())
        !HasItem("MINHP1",FourthNearestEnemyOf())
        !HasItem("IMOENHP1",FourthNearestEnemyOf())
        See(FourthNearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
    // Third Nearest.
    IF
        !RandomNum(6,3)
        OR(3)
            !HasBounceEffects(ThirdNearestEnemyOf())
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            HasItemEquiped("CLCK26",ThirdNearestEnemyOf())
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !HasItemEquiped("WA2SHIEL",ThirdNearestEnemyOf())
        !StateCheck(ThirdNearestEnemyOf(),STATE_OUT_OF_ACTION)
        CheckStat(ThirdNearestEnemyOf(),0,SANCTUARY)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            CheckStatLT(ThirdNearestEnemyOf(),50,RESISTMAGIC)
        !Name("Viconia",ThirdNearestEnemyOf())
        !HasItem("MINHP1",ThirdNearestEnemyOf())
        !HasItem("IMOENHP1",ThirdNearestEnemyOf())
        See(ThirdNearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
    // Second Nearest.
    IF
        !RandomNum(6,2)
        OR(3)
            !HasBounceEffects(SecondNearestEnemyOf())
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            HasItemEquiped("CLCK26",SecondNearestEnemyOf())
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !HasItemEquiped("WA2SHIEL",SecondNearestEnemyOf())
        !StateCheck(SecondNearestEnemyOf(),STATE_OUT_OF_ACTION)
        CheckStat(SecondNearestEnemyOf(),0,SANCTUARY)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            CheckStatLT(SecondNearestEnemyOf(),50,RESISTMAGIC)
        !Name("Viconia",SecondNearestEnemyOf())
        !HasItem("MINHP1",SecondNearestEnemyOf())
        !HasItem("IMOENHP1",SecondNearestEnemyOf())
        See(SecondNearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
    // Nearest.
    IF
        !RandomNum(6,1)
        OR(3)
            !HasBounceEffects(NearestEnemyOf())
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            HasItemEquiped("CLCK26",NearestEnemyOf())
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !HasItemEquiped("WA2SHIEL",NearestEnemyOf())
        !StateCheck(NearestEnemyOf(),STATE_OUT_OF_ACTION)
        CheckStat(NearestEnemyOf(),0,SANCTUARY)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            CheckStatLT(NearestEnemyOf(),50,RESISTMAGIC)
        !Name("Viconia",NearestEnemyOf())
        !HasItem("MINHP1",NearestEnemyOf())
        !HasItem("IMOENHP1",NearestEnemyOf())
        See(NearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
//==============================================================================
// *               BAF Name: BEHSPE01.BAF
// *           Segment Name: 07 4 Ray - 2 Paralyzation - IF.baf
    // Paralyzation Ray.
    IF
        HPGT(Myself,35)
        !GlobalTimerNotExpired("xyxUsedParalyzationRay","LOCALS")
        // Saves not too good.
        CheckStatGT(LastSeenBy(),0,SAVEVSSPELL)
        // Not immune to Hold.
        !StateCheck(LastSeenBy(),STATE_BERSERK) // Minsc Berserk
        !StateCheck(LastSeenBy(),SCRIPTINGSTATE3) // Barbarian Rage
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            !Kit(LastSeenBy(),UNDEADHUNTER)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            !Kit(LastSeenBy(),INQUISITOR)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            !HasItemEquiped("FREERING",LastSeenBy()) // Ring of Free Action
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            !HasItemEquiped("ROSSRING",LastSeenBy()) // Ring of Free Action
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            !HasItemEquiped("RING09",LastSeenBy()) // Ring of Free Action
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            !HasItemEquiped("NPPLAT",LastSeenBy()) // Firecam Full-Plate Armor
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            !HasItemEquiped("SHLD25",LastSeenBy()) // Shield of Harmony +2
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            !HasItemEquipedReal("SPER07",LastSeenBy()) // Spear of the Unicorn +2
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            !HasItemEquipedReal("SW1H27",LastSeenBy()) // Arbane's Sword +2
        // Not bouncing.
        // This ray is potentially disabling if it bounces, so be hesitant to use it while bouncing is not yet detected.
        OR(2)
            GlobalTimerExpired("xyxDoesNotSeeBounce","LOCALS")
            RandomNum(2,1)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !HasItemEquiped("WA2SHIEL",LastSeenBy()) // Shield of Balduran
        // Don't use too much on downed foes.
        OR(2)
            !StateCheck(LastSeenBy(),STATE_OUT_OF_ACTION)
            RandomNum(4,1)
        // Not totally magic resistant.
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            CheckStatLT(LastSeenBy(),100,RESISTMAGIC)
        // Not immortal.
        !HasItem("MINHP1",LastSeenBy())
        !HasItem("IMOENHP1",LastSeenBy())
// *               BAF Name: BEHSPE01.BAF
// *           Segment Name: 08 4 Ray - 2 Paralyzation - THEN.baf
    THEN
        RESPONSE #100
            SetGlobalTimer("xyxUsedParalyzationRay","LOCALS",6)
            ForceSpell(LastSeenBy(),BEHOLDER_PARALYZATION)
            // Wait(XXX)
        RESPONSE #80
            Continue()
    END
// *               BAF Name: BEHSPE01.BAF
// *           Segment Name: 09 4 Ray - 2 Fear - IF.baf
    // Fear Ray.
    IF
        HPGT(Myself,65)
        !GlobalTimerNotExpired("xyxUsedFearRay","LOCALS")
        // Saves not too good.
        CheckStatGT(LastSeenBy(),1,SAVEVSSPELL)
        // Not immune to Panic.
        // (No good way to detect Remove Fear.)
        !StateCheck(LastSeenBy(),STATE_BERSERK) // Minsc Berserk
        !StateCheck(LastSeenBy(),SCRIPTINGSTATE3) // Barbarian Rage
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            !Kit(LastSeenBy(),CAVALIER)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            !HasItemEquiped("CLCK30",LastSeenBy()) // Cloak of Bravery
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            !HasItemEquiped("MISCBC",LastSeenBy()) // Blackrazor
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            !HasItemEquiped("SW1H32",LastSeenBy()) // Dragonslayer
        // Not bouncing.
        // This ray is potentially disabling if it bounces, so be hesitant to use it while bouncing is not yet detected.
        OR(2)
            GlobalTimerExpired("xyxDoesNotSeeBounce","LOCALS")
            RandomNum(2,1)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !HasItemEquiped("WA2SHIEL",LastSeenBy()) // Shield of Balduran
        // Don't use too much on downed foes.
        OR(2)
            !StateCheck(LastSeenBy(),STATE_OUT_OF_ACTION)
            RandomNum(4,1)
        // Not totally magic resistant.
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            CheckStatLT(LastSeenBy(),100,RESISTMAGIC)
        // Not immortal.
        !HasItem("MINHP1",LastSeenBy())
        !HasItem("IMOENHP1",LastSeenBy())
// *               BAF Name: BEHSPE01.BAF
// *           Segment Name: 10 4 Ray - 2 Fear - THEN.baf
    THEN
        RESPONSE #100
            SetGlobalTimer("xyxUsedFearRay","LOCALS",6)
            ForceSpell(LastSeenBy(),BEHOLDER_FEAR)
            Wait(1)
        RESPONSE #100
            Continue()
    END
// *               BAF Name: BEHSPE01.BAF
// *           Segment Name: 11 4 Ray - 1 Charm Person - IF.baf
    // Charm Person Ray.
    IF
        HPGT(Myself,45)
        !GlobalTimerNotExpired("xyxUsedCharmPersonRay","LOCALS")
        // Must be 'Person'.
        General(LastSeenBy(),HUMANOID)
        // Saves not too good.
        CheckStatGT(LastSeenBy(),4,SAVEVSSPELL)
        // Not immune to Charm.
        // (No good way to detect Chaotic Commands.)
        !StateCheck(LastSeenBy(),STATE_BERSERK) // Minsc Berserk
        !StateCheck(LastSeenBy(),SCRIPTINGSTATE3) // Barbarian Rage
        !Class(LastSeenBy(),MONK)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            !Kit(LastSeenBy(),INQUISITOR)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            !HasItemEquiped("HELM06",LastSeenBy()) // Helm of Charm Protection
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            !HasItemEquiped("MISCBC",LastSeenBy()) // Blackrazor
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            !HasItemEquiped("NPCHAN",LastSeenBy()) // Corthala Family Armor
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            !HasItemEquiped("REAVER",LastSeenBy()) // Unholy Reaver
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            !HasItemEquiped("SHLD25",LastSeenBy()) // Shield of Harmony +2
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            !HasItemEquipedReal("SPER07",LastSeenBy()) // Spear of the Unicorn +2
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            !HasItemEquipedReal("STAF11",LastSeenBy()) // Staff of the Magi
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            !HasItemEquipedReal("SW1H35",LastSeenBy()) // Adjatha the Drinker +2
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            !HasItemEquipedReal("SW1H54",LastSeenBy()) // The Equalizer
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            !HasItemEquipedReal("SW2H14",LastSeenBy()) // Lilarcor
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            Race(LastSeenBy(),PLANATAR)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            Race(LastSeenBy(),DARKPLANATAR)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            Race(LastSeenBy(),SOLAR)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            Race(LastSeenBy(),ANTISOLAR)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !HasItemEquiped("WA2SHIEL",LastSeenBy()) // Shield of Balduran
        // Don't use too much on downed foes.
        OR(2)
            !StateCheck(LastSeenBy(),STATE_OUT_OF_ACTION)
            RandomNum(4,1)
        // Not totally magic resistant.
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            CheckStatLT(LastSeenBy(),100,RESISTMAGIC)
        // Not immortal.
        !HasItem("MINHP1",LastSeenBy())
        !HasItem("IMOENHP1",LastSeenBy())
// *               BAF Name: BEHSPE01.BAF
// *           Segment Name: 12 4 Ray - 1 Charm Person - THEN.baf
    THEN
        RESPONSE #100
            SetGlobalTimer("xyxUsedCharmPersonRay","LOCALS",6)
            ForceSpell(LastSeenBy(),BEHOLDER_CHARM_PERSON)
            Wait(4)
        RESPONSE #100
            Continue()
    END
// *               BAF Name: BEHSPE01.BAF
// *           Segment Name: 13 3 Targeting - Rays - Damage.baf
// Find a target without Spell Turning, Magic Resistance, the Shield of Balduran or the Cloak of Mirroring.
// (The cut-off resets the LastSeenBy.)
// (Improved Invisibility is no problem for ForceSpell.)
    // Sixth Nearest.
    IF
        // Randomize a bit.
        !RandomNum(6,6)
        // Not bouncing ANYTHING.
        OR(2)
            // The target isn't bouncing spells...
            !HasBounceEffects(SixthNearestEnemyOf())
            // or this Beholder hasn't caught on yet.
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
        // Not deflecting spells with the ToB Cloak of Mirroring.
        OR(2)
            // This Beholder hasn't caught on yet...
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            // or the target doesn't have the Cloak of Mirroring.
            !HasItemEquiped("CLCK26",SixthNearestEnemyOf())
        // Not already out of action.
        !StateCheck(SixthNearestEnemyOf(),STATE_OUT_OF_ACTION)
        // Not in Sanctuary.
        CheckStat(SixthNearestEnemyOf(),0,SANCTUARY)
        // Not too resistant.
        OR(2)
            // This Beholder hasn't noticed yet...
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            // or not too resistant.
            CheckStatLT(SixthNearestEnemyOf(),50,RESISTMAGIC)
        // Viconia is pretty resistant.  Beholders would know a Drow when they see one.
        !Name("Viconia",SixthNearestEnemyOf())
        // Not immortal.
        !HasItem("MINHP1",SixthNearestEnemyOf())
        !HasItem("IMOENHP1",SixthNearestEnemyOf())
        // Target.
        See(SixthNearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
    // Fifth Nearest.
    IF
        !RandomNum(6,5)
        OR(2)
            !HasBounceEffects(FifthNearestEnemyOf())
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !HasItemEquiped("CLCK26",FifthNearestEnemyOf())
        !StateCheck(FifthNearestEnemyOf(),STATE_OUT_OF_ACTION)
        CheckStat(FifthNearestEnemyOf(),0,SANCTUARY)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            CheckStatLT(FifthNearestEnemyOf(),50,RESISTMAGIC)
        !Name("Viconia",FifthNearestEnemyOf())
        !HasItem("MINHP1",FifthNearestEnemyOf())
        !HasItem("IMOENHP1",FifthNearestEnemyOf())
        See(FifthNearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
    // Fourth Nearest.
    IF
        !RandomNum(6,4)
        OR(2)
            !HasBounceEffects(FourthNearestEnemyOf())
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !HasItemEquiped("CLCK26",FourthNearestEnemyOf())
        !StateCheck(FourthNearestEnemyOf(),STATE_OUT_OF_ACTION)
        CheckStat(FourthNearestEnemyOf(),0,SANCTUARY)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            CheckStatLT(FourthNearestEnemyOf(),50,RESISTMAGIC)
        !Name("Viconia",FourthNearestEnemyOf())
        !HasItem("MINHP1",FourthNearestEnemyOf())
        !HasItem("IMOENHP1",FourthNearestEnemyOf())
        See(FourthNearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
    // Third Nearest.
    IF
        !RandomNum(6,3)
        OR(2)
            !HasBounceEffects(ThirdNearestEnemyOf())
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !HasItemEquiped("CLCK26",ThirdNearestEnemyOf())
        !StateCheck(ThirdNearestEnemyOf(),STATE_OUT_OF_ACTION)
        CheckStat(ThirdNearestEnemyOf(),0,SANCTUARY)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            CheckStatLT(ThirdNearestEnemyOf(),50,RESISTMAGIC)
        !Name("Viconia",ThirdNearestEnemyOf())
        !HasItem("MINHP1",ThirdNearestEnemyOf())
        !HasItem("IMOENHP1",ThirdNearestEnemyOf())
        See(ThirdNearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
    // Second Nearest.
    IF
        !RandomNum(6,2)
        OR(2)
            !HasBounceEffects(SecondNearestEnemyOf())
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !HasItemEquiped("CLCK26",SecondNearestEnemyOf())
        !StateCheck(SecondNearestEnemyOf(),STATE_OUT_OF_ACTION)
        CheckStat(SecondNearestEnemyOf(),0,SANCTUARY)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            CheckStatLT(SecondNearestEnemyOf(),50,RESISTMAGIC)
        !Name("Viconia",SecondNearestEnemyOf())
        !HasItem("MINHP1",SecondNearestEnemyOf())
        !HasItem("IMOENHP1",SecondNearestEnemyOf())
        See(SecondNearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
    // Nearest.
    IF
        !RandomNum(6,1)
        OR(2)
            !HasBounceEffects(NearestEnemyOf())
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !HasItemEquiped("CLCK26",NearestEnemyOf())
        !StateCheck(NearestEnemyOf(),STATE_OUT_OF_ACTION)
        CheckStat(NearestEnemyOf(),0,SANCTUARY)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            CheckStatLT(NearestEnemyOf(),50,RESISTMAGIC)
        !Name("Viconia",NearestEnemyOf())
        !HasItem("MINHP1",NearestEnemyOf())
        !HasItem("IMOENHP1",NearestEnemyOf())
        See(NearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
//==============================================================================
// *               BAF Name: BEHSPE01.BAF
// *           Segment Name: 14 4 Ray - 3 Cause Serious Wounds - IF.baf
    // Cause Serious Wounds Ray.
    IF
        HPGT(Myself,20)
        !GlobalTimerNotExpired("xyxUsedCauseSeriousWoundsRay","LOCALS")
        // Not too resistant to magic damage.
        CheckStatLT(LastSeenBy(),75,MAGICDAMAGERESISTANCE)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !HasItemEquiped("CLCK26",LastSeenBy()) // Cloak of Mirroring
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !HasItemEquiped("WA2SHIEL",LastSeenBy()) // Shield of Balduran
        // Not totally magic resistant.
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            CheckStatLT(LastSeenBy(),100,RESISTMAGIC)
        // Not immortal.
        !HasItem("MINHP1",LastSeenBy())
        !HasItem("IMOENHP1",LastSeenBy())
// *               BAF Name: BEHSPE01.BAF
// *           Segment Name: 15 4 Ray - 3 Cause Serious Wounds - THEN.baf
    THEN
        RESPONSE #100
            SetGlobalTimer("xyxUsedCauseSeriousWoundsRay","LOCALS",6)
            ForceSpell(LastSeenBy(),BEHOLDER_CAUSE_SERIOUS_WOUNDS)
            Wait(3)
        RESPONSE #20
            Continue()
    END
// *               BAF Name: BEHSPE01.BAF
// *           Segment Name: 16 6 Background Script Block - Stand Ground.baf
// Prevent background scripts from doing something stupid...
IF
    True()
THEN
    RESPONSE #100
        NoAction()
END
//==============================================================================
