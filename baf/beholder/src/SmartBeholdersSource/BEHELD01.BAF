// **********************************************************************
// *               BAF Name: BEHELD01.BAF
// *                Created: 10/01/2001 11:31:57 PM
// *       BAF Assembled by: AIScriptFactory Text
// **********************************************************************
// *               BAF Name: BEHELD01.BAF
// *           Segment Name: 00 0 Header - BEHELD01.baf
// Elder Orb
// Triggers:
//  Death Spell
//  Dispel Magic
//  Fireball
//  Melf's Acid Arrow
//  Spell Trap
//  Stoneskin x 2
//  True Sight
//  Innate Spell Turning
// Spells memorized:
//  Imprisonment
//  Maze
//  Power Word Kill
//  Spell Turning
//  True Sight
//  Vocalize
// Powers                           HPGT    Wait
// BEHOLDER_ANTIMAGIC_RAY           0       0
// BEHOLDER_CAUSE_SERIOUS_WOUNDS    5       0
// BEHOLDER_DEATH_RAY               55      0
// BEHOLDER_DOMINATION              15      0
// BEHOLDER_FLESH_TO_STONE          25      0
// BEHOLDER_SLOW                    35      2
// Spots bouncing after 3 seconds.
// Spots immunities after 12 seconds.
//==============================================================================
// *               BAF Name: BEHELD01.BAF
// *           Segment Name: 01 1 Behavior - 2 Track the Party.baf
    // Seek out the party if they're out of sight but have given themselves away.
    IF
        // No-one AROUND.
        // (Don't move if invisible players are around - it would look suspicious.)
        !Detect(Player1)
        !Detect(Player2)
        !Detect(Player3)
        !Detect(Player4)
        !Detect(Player5)
        !Detect(Player6)
        !See(NearestEnemyOf())
        // Don't move move to invisible people, unless I can see the invisible.
        OR(2)
            CheckStat(Myself,1,SEEINVISIBLE)
            !StateCheck(Player1,STATE_INVISIBLE)
        OR(2)
            CheckStat(Myself,1,SEEINVISIBLE)
            CheckStat(Player1,0,SANCTUARY)
        OR(4)
            // They were here...
            Global("xyxInitialized","LOCALS",1)
            // or someone called for reinforcements...
            // (This Shout number is used in GENSHT01.BCS, the override script for all Beholders.)
            // (GENSHT.BCS moves the Beholder up to another Beholder, not necessarily up to the party.)
            Heard([ANYONE],89)
            // (This Shout is used in WTASIGHT.BCS and WTARSGT.BCS.)
            Heard([ANYONE],111)
            // or I am taking damage from an unseen source (such as off-screen Cloudkilling).
            HitBy([ANYTHING],DEFAULT)
        // I can see where the damage or Shout came from.
        !StateCheck(Myself,STATE_BLIND)
        // Not charmed.
        !StateCheck(Myself,STATE_CHARMED)
    THEN
        RESPONSE #100
            // (The lack of ActionListEmpty checks means the Beholder won't go any closer than needed.)
            MoveToObject(Player1)
    END
// *               BAF Name: BEHELD01.BAF
// *           Segment Name: 02 1 Behavior - 3 Investigate.baf
    // Move around when something suspicious happens (such as off-screen Cloudkilling).
    IF
        // No-one to be SEEN.
        !See(Player1)
        !See(Player2)
        !See(Player3)
        !See(Player4)
        !See(Player5)
        !See(Player6)
        !See(NearestEnemyOf())
        OR(4)
            // They were here...
            Global("xyxInitialized","LOCALS",1)
            // or someone called for reinforcements...
            // (This Shout number is used in GENSHT01.BCS, the override script for all Beholders.)
            // (GENSHT.BCS moves the Beholder up to another Beholder, not necessarily up to the party.)
            Heard([ANYONE],89)
            // (This Shout is used in WTASIGHT.BCS and WTARSGT.BCS.)
            Heard([ANYONE],111)
            // or I am taking damage from an unseen source (such as off-screen Cloudkilling).
            HitBy([ANYTHING],DEFAULT)
        !StateCheck(Myself,STATE_CHARMED)
    THEN
        RESPONSE #100
            // Move around.
            RandomWalkContinuous()
    END
// *               BAF Name: BEHELD01.BAF
// *           Segment Name: 03 1 Behavior - 4 Cut-Off - Stand Ground.baf
    // Performance saver.
    IF
        // Target Player1-6 in case any of them is Charmed.
        !See(Player1)
        !See(Player2)
        !See(Player3)
        !See(Player4)
        !See(Player5)
        !See(Player6)
        !See(NearestEnemyOf())
    THEN
        RESPONSE #100
            // Restart
    END
// *               BAF Name: BEHELD01.BAF
// *           Segment Name: 04 1 Behavior - 5 Initialize - 1 Bounce.baf
    // Initialize.
    IF
        // This only runs if there are GOODCUTOFF visible.
        Global("xyxInitialized","LOCALS",0)
    THEN
        RESPONSE #100
            // Call for reinforcements  (Don't wait until attacked...).
            // (This Shout number is used in GENSHT01.BCS, the override script for all Beholders.)
            Shout(89)
            // Beholders are too smart to target a bouncing or immune character after they notice it.
            // Notice bouncing after one round.
            SetGlobalTimer("xyxDoesNotSeeBounce","LOCALS",3)
// *               BAF Name: BEHELD01.BAF
// *           Segment Name: 05 1 Behavior - 5 Initialize - 2 Immunities.baf
            // Notice immunity after a couple of rounds.
            SetGlobalTimer("xyxDoesNotSeeImmunity","LOCALS",12)
            SetGlobal("xyxInitialized","LOCALS",1)
            Continue()
    END
//==============================================================================
// *               BAF Name: BEHELD01.BAF
// *           Segment Name: 06 2 Contingencies - BEHELD01.baf
// BEHELD01 Triggers
    // Chain Contingency.
    IF
        Global("xyxChainContingency","LOCALS",0)
    THEN
        RESPONSE #100
            SetGlobal("xyxChainContingency","LOCALS",1)
            DisplayString(Myself,26328) // Chain Contingency
            DisplayString(Myself,8300) // Stoneskin
            ApplySpell(Myself,WIZARD_STONE_SKIN)
            DisplayString(Myself,25633) // True Sight
            SetGlobalTimer("xyxTrueSight","LOCALS",60) // 10 rounds
            ApplySpell(Myself,WIZARD_TRUE_SIGHT)
            // Spell Trap displays its own string.
            ApplySpell(Myself,WIZARD_SPELL_TRAP)
            Continue()
    END
    // Spell Trigger.
    IF
        !GlobalTimerNotExpired("xyxSpellCast","LOCALS")
        Global("xyxSpellTrigger","LOCALS",0)
        OR(5)
            // Stoneskin has worn off...
            CheckStat(Myself,0,STONESKINS)
            // Spell Trap was dispelled...
            SpellCastOnMe([ANYONE],WIZARD_RUBY_RAY_OF_REVERSAL)
            SpellCastOnMe([ANYONE],WIZARD_PIERCE_SHIELD)
            SpellCastOnMe([ANYONE],WIZARD_SPELL_STRIKE)
            // or there are several summoned creatures...
            NumCreatureGT([GOODCUTOFF.0.0.0.0.SUMMONED],2)
        OR(5)
            CheckStat(Myself,0,STONESKINS)
            SpellCastOnMe([ANYONE],WIZARD_RUBY_RAY_OF_REVERSAL)
            SpellCastOnMe([ANYONE],WIZARD_PIERCE_SHIELD)
            SpellCastOnMe([ANYONE],WIZARD_SPELL_STRIKE)
            // in the area.
            Range(SecondNearest([GOODCUTOFF.0.0.0.0.SUMMONED]),16)
    THEN
        RESPONSE #100
            SetGlobal("xyxSpellTrigger","LOCALS",1)
            SetGlobalTimer("xyxSpellCast","LOCALS",6)
            DisplayString(Myself,26243) // Spell Trigger
            DisplayString(Myself,8300) // Stoneskin
            ApplySpell(Myself,WIZARD_STONE_SKIN)
            DisplayString(Myself,8273) // Spell Turning
            ApplySpell(Myself,BEHOLDER_SPELL_TURNING)
            // Death Spell displays its own string.
            ReallyForceSpell(Myself,WIZARD_DEATH_SPELL)
    END
    // Spell Sequencer.
    IF
        !GlobalTimerNotExpired("xyxSpellCast","LOCALS")
        Global("xyxSpellSequencer","LOCALS",0)
        !Range(TenthNearestEnemyOf(),16) // minimum
        OR(3)
            // No-one will bounce the Fireball...
            !PartyHasItem("CLCK26") // Cloak of Mirroring
            // the Beholder has not yet noticed BounceEffects...
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            // or there are at least four potential victims, in which case it's worth it.
            NumCreatureGT([GOODCUTOFF],3)
        OR(3)
            // The target will not bounce Melf's Acid Arrow.
            !HasItemEquiped("CLCK26",TenthNearestEnemyOf()) // Cloak of Mirroring
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
    THEN
        RESPONSE #100
            SetGlobal("xyxSpellSequencer","LOCALS",1)
            SetGlobalTimer("xyxSpellCast","LOCALS",6)
            DisplayString(Myself,25951) // Spell Sequencer
            // Fireball displays its own string.
            ReallyForceSpell(TenthNearestEnemyOf(),WIZARD_FIREBALL)
            // Dispel Magic displays its own string.
            ReallyForceSpell(TenthNearestEnemyOf(),WIZARD_DISPEL_MAGIC)
            // Melf's Acid Arrow displays its own string.
            ReallyForceSpell(TenthNearestEnemyOf(),WIZARD_MELF_ACID_ARROW)
    END
//==============================================================================
// *               BAF Name: BEHELD01.BAF
// *           Segment Name: 07 3 Targeting - Rays - Disabling.baf
// Find a target without Spell Turning, Magic Resistance or the Shield of Balduran.
// (The cut-off resets the LastSeenBy.)
// (Improved Invisibility is no problem for ForceSpell.)
    // Sixth Nearest.
    IF
        // Randomize a bit.
        !RandomNum(6,6)
        // Not bouncing ALL spells.
        OR(3)
            // The target isn't bouncing spells...
            !HasBounceEffects(SixthNearestEnemyOf())
            // or this Beholder hasn't caught on yet...
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            // or the target has the Cloak of Mirroring.
            HasItemEquiped("CLCK26",SixthNearestEnemyOf())
        OR(2)
            // This Beholder hasn't caught on yet...
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            // or the target doesn't have the Shield of Balduran.
            !HasItemEquiped("WA2SHIEL",SixthNearestEnemyOf())
        // Not already out of action.
        !StateCheck(SixthNearestEnemyOf(),STATE_OUT_OF_ACTION)
        // Not in Sanctuary.
        CheckStat(SixthNearestEnemyOf(),0,SANCTUARY)
        // Not too resistant.
        OR(2)
            // This Beholder hasn't noticed yet...
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            // or not too resistant.
            CheckStatLT(SixthNearestEnemyOf(),50,RESISTMAGIC)
        // Viconia is pretty resistant.  Beholders would know a Drow when they see one.
        !Name("Viconia",SixthNearestEnemyOf())
        // Not immortal.
        !HasItem("MINHP1",SixthNearestEnemyOf())
        !HasItem("IMOENHP1",SixthNearestEnemyOf())
        // Target.
        See(SixthNearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
    // Fifth Nearest.
    IF
        !RandomNum(6,5)
        OR(3)
            !HasBounceEffects(FifthNearestEnemyOf())
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            HasItemEquiped("CLCK26",FifthNearestEnemyOf())
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !HasItemEquiped("WA2SHIEL",FifthNearestEnemyOf())
        !StateCheck(FifthNearestEnemyOf(),STATE_OUT_OF_ACTION)
        CheckStat(FifthNearestEnemyOf(),0,SANCTUARY)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            CheckStatLT(FifthNearestEnemyOf(),50,RESISTMAGIC)
        !Name("Viconia",FifthNearestEnemyOf())
        !HasItem("MINHP1",FifthNearestEnemyOf())
        !HasItem("IMOENHP1",FifthNearestEnemyOf())
        See(FifthNearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
    // Fourth Nearest.
    IF
        !RandomNum(6,4)
        OR(3)
            !HasBounceEffects(FourthNearestEnemyOf())
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            HasItemEquiped("CLCK26",FourthNearestEnemyOf())
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !HasItemEquiped("WA2SHIEL",FourthNearestEnemyOf())
        !StateCheck(FourthNearestEnemyOf(),STATE_OUT_OF_ACTION)
        CheckStat(FourthNearestEnemyOf(),0,SANCTUARY)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            CheckStatLT(FourthNearestEnemyOf(),50,RESISTMAGIC)
        !Name("Viconia",FourthNearestEnemyOf())
        !HasItem("MINHP1",FourthNearestEnemyOf())
        !HasItem("IMOENHP1",FourthNearestEnemyOf())
        See(FourthNearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
    // Third Nearest.
    IF
        !RandomNum(6,3)
        OR(3)
            !HasBounceEffects(ThirdNearestEnemyOf())
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            HasItemEquiped("CLCK26",ThirdNearestEnemyOf())
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !HasItemEquiped("WA2SHIEL",ThirdNearestEnemyOf())
        !StateCheck(ThirdNearestEnemyOf(),STATE_OUT_OF_ACTION)
        CheckStat(ThirdNearestEnemyOf(),0,SANCTUARY)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            CheckStatLT(ThirdNearestEnemyOf(),50,RESISTMAGIC)
        !Name("Viconia",ThirdNearestEnemyOf())
        !HasItem("MINHP1",ThirdNearestEnemyOf())
        !HasItem("IMOENHP1",ThirdNearestEnemyOf())
        See(ThirdNearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
    // Second Nearest.
    IF
        !RandomNum(6,2)
        OR(3)
            !HasBounceEffects(SecondNearestEnemyOf())
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            HasItemEquiped("CLCK26",SecondNearestEnemyOf())
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !HasItemEquiped("WA2SHIEL",SecondNearestEnemyOf())
        !StateCheck(SecondNearestEnemyOf(),STATE_OUT_OF_ACTION)
        CheckStat(SecondNearestEnemyOf(),0,SANCTUARY)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            CheckStatLT(SecondNearestEnemyOf(),50,RESISTMAGIC)
        !Name("Viconia",SecondNearestEnemyOf())
        !HasItem("MINHP1",SecondNearestEnemyOf())
        !HasItem("IMOENHP1",SecondNearestEnemyOf())
        See(SecondNearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
    // Nearest.
    IF
        !RandomNum(6,1)
        OR(3)
            !HasBounceEffects(NearestEnemyOf())
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            HasItemEquiped("CLCK26",NearestEnemyOf())
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !HasItemEquiped("WA2SHIEL",NearestEnemyOf())
        !StateCheck(NearestEnemyOf(),STATE_OUT_OF_ACTION)
        CheckStat(NearestEnemyOf(),0,SANCTUARY)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            CheckStatLT(NearestEnemyOf(),50,RESISTMAGIC)
        !Name("Viconia",NearestEnemyOf())
        !HasItem("MINHP1",NearestEnemyOf())
        !HasItem("IMOENHP1",NearestEnemyOf())
        See(NearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
//==============================================================================
// *               BAF Name: BEHELD01.BAF
// *           Segment Name: 08 4 Ray - 1 Domination - IF.baf
    // Domination Ray.
    IF
        HPGT(Myself,15)
        !GlobalTimerNotExpired("xyxUsedDominationRay","LOCALS")
        // Saves not too good.
        CheckStatGT(LastSeenBy(),-1,SAVEVSSPELL)
        // Not immune to Charm.
        // (No good way to detect Chaotic Commands.)
        !StateCheck(LastSeenBy(),STATE_BERSERK) // Minsc Berserk
        !StateCheck(LastSeenBy(),SCRIPTINGSTATE3) // Barbarian Rage
        !Class(LastSeenBy(),MONK)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            !Kit(LastSeenBy(),INQUISITOR)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            !HasItemEquiped("HELM06",LastSeenBy()) // Helm of Charm Protection
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            !HasItemEquiped("MISCBC",LastSeenBy()) // Blackrazor
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            !HasItemEquiped("NPCHAN",LastSeenBy()) // Corthala Family Armor
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            !HasItemEquiped("REAVER",LastSeenBy()) // Unholy Reaver
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            !HasItemEquiped("SHLD25",LastSeenBy()) // Shield of Harmony +2
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            !HasItemEquipedReal("SPER07",LastSeenBy()) // Spear of the Unicorn +2
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            !HasItemEquipedReal("STAF11",LastSeenBy()) // Staff of the Magi
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            !HasItemEquipedReal("SW1H35",LastSeenBy()) // Adjatha the Drinker +2
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            !HasItemEquipedReal("SW1H54",LastSeenBy()) // The Equalizer
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            !HasItemEquipedReal("SW2H14",LastSeenBy()) // Lilarcor
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            Race(LastSeenBy(),PLANATAR)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            Race(LastSeenBy(),DARKPLANATAR)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            Race(LastSeenBy(),SOLAR)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            Race(LastSeenBy(),ANTISOLAR)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !HasItemEquiped("WA2SHIEL",LastSeenBy()) // Shield of Balduran
        // Don't use too much on downed foes.
        OR(2)
            !StateCheck(LastSeenBy(),STATE_OUT_OF_ACTION)
            RandomNum(4,1)
        // Not totally magic resistant.
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            CheckStatLT(LastSeenBy(),100,RESISTMAGIC)
        // Not immortal.
        !HasItem("MINHP1",LastSeenBy())
        !HasItem("IMOENHP1",LastSeenBy())
// *               BAF Name: BEHELD01.BAF
// *           Segment Name: 09 4 Ray - 1 Domination - THEN.baf
    THEN
        RESPONSE #100
            SetGlobalTimer("xyxUsedDominationRay","LOCALS",6)
            ForceSpell(LastSeenBy(),BEHOLDER_DOMINATION)
            // Wait(XXX)
        RESPONSE #80
            Continue()
    END
// *               BAF Name: BEHELD01.BAF
// *           Segment Name: 10 3 Targeting - Rays - Damage.baf
// Find a target without Spell Turning, Magic Resistance, the Shield of Balduran or the Cloak of Mirroring.
// (The cut-off resets the LastSeenBy.)
// (Improved Invisibility is no problem for ForceSpell.)
    // Sixth Nearest.
    IF
        // Randomize a bit.
        !RandomNum(6,6)
        // Not bouncing ANYTHING.
        OR(2)
            // The target isn't bouncing spells...
            !HasBounceEffects(SixthNearestEnemyOf())
            // or this Beholder hasn't caught on yet.
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
        // Not deflecting spells with the ToB Cloak of Mirroring.
        OR(2)
            // This Beholder hasn't caught on yet...
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            // or the target doesn't have the Cloak of Mirroring.
            !HasItemEquiped("CLCK26",SixthNearestEnemyOf())
        // Not already out of action.
        !StateCheck(SixthNearestEnemyOf(),STATE_OUT_OF_ACTION)
        // Not in Sanctuary.
        CheckStat(SixthNearestEnemyOf(),0,SANCTUARY)
        // Not too resistant.
        OR(2)
            // This Beholder hasn't noticed yet...
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            // or not too resistant.
            CheckStatLT(SixthNearestEnemyOf(),50,RESISTMAGIC)
        // Viconia is pretty resistant.  Beholders would know a Drow when they see one.
        !Name("Viconia",SixthNearestEnemyOf())
        // Not immortal.
        !HasItem("MINHP1",SixthNearestEnemyOf())
        !HasItem("IMOENHP1",SixthNearestEnemyOf())
        // Target.
        See(SixthNearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
    // Fifth Nearest.
    IF
        !RandomNum(6,5)
        OR(2)
            !HasBounceEffects(FifthNearestEnemyOf())
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !HasItemEquiped("CLCK26",FifthNearestEnemyOf())
        !StateCheck(FifthNearestEnemyOf(),STATE_OUT_OF_ACTION)
        CheckStat(FifthNearestEnemyOf(),0,SANCTUARY)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            CheckStatLT(FifthNearestEnemyOf(),50,RESISTMAGIC)
        !Name("Viconia",FifthNearestEnemyOf())
        !HasItem("MINHP1",FifthNearestEnemyOf())
        !HasItem("IMOENHP1",FifthNearestEnemyOf())
        See(FifthNearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
    // Fourth Nearest.
    IF
        !RandomNum(6,4)
        OR(2)
            !HasBounceEffects(FourthNearestEnemyOf())
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !HasItemEquiped("CLCK26",FourthNearestEnemyOf())
        !StateCheck(FourthNearestEnemyOf(),STATE_OUT_OF_ACTION)
        CheckStat(FourthNearestEnemyOf(),0,SANCTUARY)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            CheckStatLT(FourthNearestEnemyOf(),50,RESISTMAGIC)
        !Name("Viconia",FourthNearestEnemyOf())
        !HasItem("MINHP1",FourthNearestEnemyOf())
        !HasItem("IMOENHP1",FourthNearestEnemyOf())
        See(FourthNearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
    // Third Nearest.
    IF
        !RandomNum(6,3)
        OR(2)
            !HasBounceEffects(ThirdNearestEnemyOf())
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !HasItemEquiped("CLCK26",ThirdNearestEnemyOf())
        !StateCheck(ThirdNearestEnemyOf(),STATE_OUT_OF_ACTION)
        CheckStat(ThirdNearestEnemyOf(),0,SANCTUARY)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            CheckStatLT(ThirdNearestEnemyOf(),50,RESISTMAGIC)
        !Name("Viconia",ThirdNearestEnemyOf())
        !HasItem("MINHP1",ThirdNearestEnemyOf())
        !HasItem("IMOENHP1",ThirdNearestEnemyOf())
        See(ThirdNearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
    // Second Nearest.
    IF
        !RandomNum(6,2)
        OR(2)
            !HasBounceEffects(SecondNearestEnemyOf())
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !HasItemEquiped("CLCK26",SecondNearestEnemyOf())
        !StateCheck(SecondNearestEnemyOf(),STATE_OUT_OF_ACTION)
        CheckStat(SecondNearestEnemyOf(),0,SANCTUARY)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            CheckStatLT(SecondNearestEnemyOf(),50,RESISTMAGIC)
        !Name("Viconia",SecondNearestEnemyOf())
        !HasItem("MINHP1",SecondNearestEnemyOf())
        !HasItem("IMOENHP1",SecondNearestEnemyOf())
        See(SecondNearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
    // Nearest.
    IF
        !RandomNum(6,1)
        OR(2)
            !HasBounceEffects(NearestEnemyOf())
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !HasItemEquiped("CLCK26",NearestEnemyOf())
        !StateCheck(NearestEnemyOf(),STATE_OUT_OF_ACTION)
        CheckStat(NearestEnemyOf(),0,SANCTUARY)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            CheckStatLT(NearestEnemyOf(),50,RESISTMAGIC)
        !Name("Viconia",NearestEnemyOf())
        !HasItem("MINHP1",NearestEnemyOf())
        !HasItem("IMOENHP1",NearestEnemyOf())
        See(NearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
//==============================================================================
// *               BAF Name: BEHELD01.BAF
// *           Segment Name: 11 4 Ray - 1 Death Ray - IF.baf
    // Death Ray.
    IF
        HPGT(Myself,55)
        !GlobalTimerNotExpired("xyxUsedDeathRay","LOCALS")
        // Saves not too good.
        CheckStatGT(LastSeenBy(),1,SAVEVSDEATH)
        // Not immune to Death.
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !Race(LastSeenBy(),PLANATAR)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !Race(LastSeenBy(),DARKPLANATAR)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !Race(LastSeenBy(),SOLAR)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !Race(LastSeenBy(),ANTISOLAR)
        // Not bouncing.
        // This ray is potentially deadly if it bounces, so be hesitant to use it while bouncing is not yet detected.
        OR(2)
            GlobalTimerExpired("xyxDoesNotSeeBounce","LOCALS")
            RandomNum(3,1)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !HasItemEquiped("WA2SHIEL",LastSeenBy()) // Shield of Balduran
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !HasItemEquiped("CLCK26",LastSeenBy()) // Cloak of Mirroring
        // Not totally magic resistant.
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            CheckStatLT(LastSeenBy(),100,RESISTMAGIC)
        // Not immortal.
        !HasItem("MINHP1",LastSeenBy())
        !HasItem("IMOENHP1",LastSeenBy())
// *               BAF Name: BEHELD01.BAF
// *           Segment Name: 12 4 Ray - 1 Death Ray - THEN.baf
    THEN
        RESPONSE #100
            SetGlobalTimer("xyxUsedDeathRay","LOCALS",6)
            ForceSpell(LastSeenBy(),BEHOLDER_DEATH_RAY)
            // Wait(XXX)
        RESPONSE #100
            Continue()
    END
// *               BAF Name: BEHELD01.BAF
// *           Segment Name: 13 4 Ray - 1 Flesh to Stone - IF.baf
    // Flesh to Stone Ray.
    IF
        HPGT(Myself,25)
        !GlobalTimerNotExpired("xyxUsedFleshToStoneRay","LOCALS")
        // Saves not too good.
        CheckStatGT(LastSeenBy(),1,SAVEVSSPELL)
        // Not immune to Petrification.
        // (No good way to detect Protection from Petrification.)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !Race(LastSeenBy(),PLANATAR)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !Race(LastSeenBy(),DARKPLANATAR)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !Race(LastSeenBy(),SOLAR)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !Race(LastSeenBy(),ANTISOLAR)
        // Not bouncing.
        // This ray is potentially deadly if it bounces, so be hesitant to use it while bouncing is not yet detected.
        OR(2)
            GlobalTimerExpired("xyxDoesNotSeeBounce","LOCALS")
            RandomNum(3,1)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !HasItemEquiped("WA2SHIEL",LastSeenBy()) // Shield of Balduran
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !HasItemEquiped("CLCK26",LastSeenBy()) // Cloak of Mirroring
        // Not totally magic resistant.
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            CheckStatLT(LastSeenBy(),100,RESISTMAGIC)
        // Not immortal.
        !HasItem("MINHP1",LastSeenBy())
        !HasItem("IMOENHP1",LastSeenBy())
        // Avoid breaking up romances a bit.
        OR(4)
            // The target is not Aerie...
            !Name("Aerie",LastSeenBy())
            // or the romance hasn't started...
            GlobalLT("AerieRomanceActive","GLOBAL",2)
            // or the romance is broken up...
            GlobalGT("AerieRomanceActive","GLOBAL",2)
            // or I'm feeling nasty.
            RandomNum(4,1)
        OR(4)
            !Name("Jaheira",LastSeenBy())
            GlobalLT("JaheiraRomanceActive","GLOBAL",2)
            GlobalGT("JaheiraRomanceActive","GLOBAL",2)
            RandomNum(4,1)
        OR(4)
            !Name("Viconia",LastSeenBy())
            GlobalLT("ViconiaRomanceActive","GLOBAL",2)
            GlobalGT("ViconiaRomanceActive","GLOBAL",2)
            RandomNum(4,1)
        OR(4)
            !Name("Anomen",LastSeenBy())
            GlobalLT("AnomenRomanceActive","GLOBAL",2)
            GlobalGT("AnomenRomanceActive","GLOBAL",2)
            RandomNum(4,1)
// *               BAF Name: BEHELD01.BAF
// *           Segment Name: 14 4 Ray - 1 Flesh to Stone - THEN.baf
    THEN
        RESPONSE #100
            SetGlobalTimer("xyxUsedFleshToStoneRay","LOCALS",6)
            ForceSpell(LastSeenBy(),BEHOLDER_FLESH_TO_STONE)
            // Wait(XXX)
        RESPONSE #100
            Continue()
    END
// *               BAF Name: BEHELD01.BAF
// *           Segment Name: 15 4 Spell - 1 Imprisonment.baf
// Find a target without Spell Turning or Magic Resistance.
// (The cut-off resets the LastSeenBy.)
    // Sixth nearest.
    IF
        !GlobalTimerNotExpired("xyxSpellCast","LOCALS")
        HaveSpell(WIZARD_IMPRISONMENT)
        // Randomize a bit.
        !RandomNum(6,6)
        // Target not too far.
        Range(SixthNearestEnemyOf(),10)
        // Only imprison worthwile targets.
        CheckStatGT(SixthNearestEnemyOf(),7,LEVEL)
        // Not immune to Imprisonment.
        // Rage is pretty obvious.
        !StateCheck(SixthNearestEnemyOf(),STATE_BERSERK) // Minsc's Berserk
        // Don't try on berserkers.  Chances are that they will use their berserk ability when possible, anyway.
        // (No way to detect the Berserker's Rage.)
        !Kit(SixthNearestEnemyOf(Myself),BERSERKER)
        // Dangerous spell that warrants contemplation; don't use before bouncing is known.
        GlobalTimerExpired("xyxDoesNotSeeBounce","LOCALS")
        // Not bouncing spells.
        OR(2)
            !HasBounceEffects(SixthNearestEnemyOf())
            // Hopefully not carrying the Shield AND turning spells; quite a gamble!
            HasItemEquiped("WA2SHIEL",SixthNearestEnemyOf())
        // Legal target.
        !StateCheck(SixthNearestEnemyOf(),STATE_IMPROVEDINVISIBILITY)
        CheckStat(SixthNearestEnemyOf(),0,SANCTUARY)
        !HasItem("MINHP1",SixthNearestEnemyOf())
        !HasItem("IMOENHP1",SixthNearestEnemyOf())
        See(SixthNearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
    // Fifth nearest.
    IF
        !GlobalTimerNotExpired("xyxSpellCast","LOCALS")
        HaveSpell(WIZARD_IMPRISONMENT)
        !RandomNum(6,5)
        Range(FifthNearestEnemyOf(),10)
        CheckStatGT(FifthNearestEnemyOf(),7,LEVEL)
        !StateCheck(FifthNearestEnemyOf(),STATE_BERSERK) // Minsc's Berserk
        !Kit(FifthNearestEnemyOf(Myself),BERSERKER)
        GlobalTimerExpired("xyxDoesNotSeeBounce","LOCALS")
        OR(2)
            !HasBounceEffects(FifthNearestEnemyOf())
            HasItemEquiped("WA2SHIEL",FifthNearestEnemyOf())
        !StateCheck(FifthNearestEnemyOf(),STATE_IMPROVEDINVISIBILITY)
        CheckStat(FifthNearestEnemyOf(),0,SANCTUARY)
        !HasItem("MINHP1",FifthNearestEnemyOf())
        !HasItem("IMOENHP1",FifthNearestEnemyOf())
        See(FifthNearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
    // Fourth nearest.
    IF
        !GlobalTimerNotExpired("xyxSpellCast","LOCALS")
        HaveSpell(WIZARD_IMPRISONMENT)
        !RandomNum(6,4)
        Range(FourthNearestEnemyOf(),10)
        CheckStatGT(FourthNearestEnemyOf(),7,LEVEL)
        !StateCheck(FourthNearestEnemyOf(),STATE_BERSERK) // Minsc's Berserk
        !Kit(FourthNearestEnemyOf(Myself),BERSERKER)
        GlobalTimerExpired("xyxDoesNotSeeBounce","LOCALS")
        OR(2)
            !HasBounceEffects(FourthNearestEnemyOf())
            HasItemEquiped("WA2SHIEL",FourthNearestEnemyOf())
        !StateCheck(FourthNearestEnemyOf(),STATE_IMPROVEDINVISIBILITY)
        CheckStat(FourthNearestEnemyOf(),0,SANCTUARY)
        !HasItem("MINHP1",FourthNearestEnemyOf())
        !HasItem("IMOENHP1",FourthNearestEnemyOf())
        See(FourthNearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
    // Third nearest.
    IF
        !GlobalTimerNotExpired("xyxSpellCast","LOCALS")
        HaveSpell(WIZARD_IMPRISONMENT)
        !RandomNum(6,3)
        Range(ThirdNearestEnemyOf(),10)
        CheckStatGT(ThirdNearestEnemyOf(),7,LEVEL)
        !StateCheck(ThirdNearestEnemyOf(),STATE_BERSERK) // Minsc's Berserk
        !Kit(ThirdNearestEnemyOf(Myself),BERSERKER)
        GlobalTimerExpired("xyxDoesNotSeeBounce","LOCALS")
        OR(2)
            !HasBounceEffects(ThirdNearestEnemyOf())
            HasItemEquiped("WA2SHIEL",ThirdNearestEnemyOf())
        !StateCheck(ThirdNearestEnemyOf(),STATE_IMPROVEDINVISIBILITY)
        CheckStat(ThirdNearestEnemyOf(),0,SANCTUARY)
        !HasItem("MINHP1",ThirdNearestEnemyOf())
        !HasItem("IMOENHP1",ThirdNearestEnemyOf())
        See(ThirdNearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
    // Second nearest.
    IF
        !GlobalTimerNotExpired("xyxSpellCast","LOCALS")
        HaveSpell(WIZARD_IMPRISONMENT)
        !RandomNum(6,2)
        Range(SecondNearestEnemyOf(),10)
        CheckStatGT(SecondNearestEnemyOf(),7,LEVEL)
        !StateCheck(SecondNearestEnemyOf(),STATE_BERSERK) // Minsc's Berserk
        !Kit(SecondNearestEnemyOf(Myself),BERSERKER)
        GlobalTimerExpired("xyxDoesNotSeeBounce","LOCALS")
        OR(2)
            !HasBounceEffects(SecondNearestEnemyOf())
            HasItemEquiped("WA2SHIEL",SecondNearestEnemyOf())
        !StateCheck(SecondNearestEnemyOf(),STATE_IMPROVEDINVISIBILITY)
        CheckStat(SecondNearestEnemyOf(),0,SANCTUARY)
        !HasItem("MINHP1",SecondNearestEnemyOf())
        !HasItem("IMOENHP1",SecondNearestEnemyOf())
        See(SecondNearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
    // Nearest.
    IF
        !GlobalTimerNotExpired("xyxSpellCast","LOCALS")
        HaveSpell(WIZARD_IMPRISONMENT)
        !RandomNum(6,1)
        Range(NearestEnemyOf(),10)
        CheckStatGT(NearestEnemyOf(),7,LEVEL)
        !StateCheck(NearestEnemyOf(),STATE_BERSERK) // Minsc's Berserk
        !Kit(NearestEnemyOf(Myself),BERSERKER)
        GlobalTimerExpired("xyxDoesNotSeeBounce","LOCALS")
        OR(2)
            !HasBounceEffects(NearestEnemyOf())
            HasItemEquiped("WA2SHIEL",NearestEnemyOf())
        !StateCheck(NearestEnemyOf(),STATE_IMPROVEDINVISIBILITY)
        CheckStat(NearestEnemyOf(),0,SANCTUARY)
        !HasItem("MINHP1",NearestEnemyOf())
        !HasItem("IMOENHP1",NearestEnemyOf())
        See(NearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
// Imprisonment.
IF
    !GlobalTimerNotExpired("xyxSpellCast","LOCALS")
    HaveSpell(WIZARD_IMPRISONMENT)
    Range(LastSeenBy(),10)
    CheckStatGT(LastSeenBy(),7,LEVEL)
    !StateCheck(LastSeenBy(),STATE_BERSERK) // Minsc's Berserk
    !Kit(LastSeenBy(Myself),BERSERKER)
    GlobalTimerExpired("xyxDoesNotSeeBounce","LOCALS")
    OR(2)
        !HasBounceEffects(LastSeenBy())
        HasItemEquiped("WA2SHIEL",LastSeenBy())
    !StateCheck(LastSeenBy(),STATE_IMPROVEDINVISIBILITY)
    CheckStat(LastSeenBy(),0,SANCTUARY)
    !HasItem("MINHP1",LastSeenBy())
    !HasItem("IMOENHP1",LastSeenBy())
    // Avoid breaking up romances a bit.
    OR(4)
        // The target is not Aerie...
        !Name("Aerie",LastSeenBy())
        // or the romance hasn't started...
        GlobalLT("AerieRomanceActive","GLOBAL",2)
        // or the romance is broken up...
        GlobalGT("AerieRomanceActive","GLOBAL",2)
        // or I'm feeling nasty.
        RandomNum(4,1)
    OR(4)
        !Name("Jaheira",LastSeenBy())
        GlobalLT("JaheiraRomanceActive","GLOBAL",2)
        GlobalGT("JaheiraRomanceActive","GLOBAL",2)
        RandomNum(4,1)
    OR(4)
        !Name("Viconia",LastSeenBy())
        GlobalLT("ViconiaRomanceActive","GLOBAL",2)
        GlobalGT("ViconiaRomanceActive","GLOBAL",2)
        RandomNum(4,1)
    OR(4)
        !Name("Anomen",LastSeenBy())
        GlobalLT("AnomenRomanceActive","GLOBAL",2)
        GlobalGT("AnomenRomanceActive","GLOBAL",2)
        RandomNum(4,1)
    // Avoid Imprisoning the protagonist a bit.
    OR(3)
        // The target is not the protagonist...
        !InPartySlot(LastSeenBy(),0)
        // or this is a solo game...
        NumInParty(1)
        // or I'm feeling nasty.
        RandomNum(4,1)
THEN
    RESPONSE #100
        // Must be next to target or spell will be interrupted by attack.
        MoveToObject(LastSeenBy())
        SetGlobalTimer("xyxSpellCast","LOCALS",6)
        Spell(LastSeenBy(),WIZARD_IMPRISONMENT)
    // Don't ALWAYS cast.  Spell Immunity - Abjuration prevents Imprisonment.
    RESPONSE #100
        Continue()
END
//==============================================================================
// *               BAF Name: BEHELD01.BAF
// *           Segment Name: 16 4 Spell - 1 Power Word Kill.baf
//==============================================================================
// Find a target without Spell Turning, Magic Resistance or the Cloak of Mirroring,
//  that has 60 or less Hit Points.
// (The cut-off resets the LastSeenBy.)
    // Sixth Nearest.
    IF
        !GlobalTimerNotExpired("xyxSpellCast","LOCALS")
        HaveSpell(WIZARD_POWER_WORD_KILL)
        // Randomize a bit.
        !RandomNum(6,6)
        HPLT(SixthNearestEnemyOf(),61)
        // Not bouncing ALL spells.
        OR(3)
            // The target isn't bouncing spells...
            !HasBounceEffects(SixthNearestEnemyOf())
            // or this Beholder hasn't caught on yet...
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            // or the target has the Shield of Balduran.
            HasItemEquiped("WA2SHIEL",SixthNearestEnemyOf())
        OR(2)
            // This Beholder hasn't caught on yet...
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            // or the target doesn't have the Cloak of Mirroring (which bounces Power Word Kill...).
            !HasItemEquiped("CLCK26",SixthNearestEnemyOf())
        // Not immune to Death.
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            !StateCheck(SixthNearestEnemyOf(),SCRIPTINGSTATE2) // Death Ward
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !Race(SixthNearestEnemyOf(),PLANATAR)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !Race(SixthNearestEnemyOf(),DARKPLANATAR)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !Race(SixthNearestEnemyOf(),SOLAR)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !Race(SixthNearestEnemyOf(),ANTISOLAR)
        // Not already out of action.
        !StateCheck(SixthNearestEnemyOf(),STATE_OUT_OF_ACTION)
        // Not Improved Invisible or in Sanctuary.
        !StateCheck(SixthNearestEnemyOf(),STATE_IMPROVEDINVISIBILITY)
        CheckStat(SixthNearestEnemyOf(),0,SANCTUARY)
        // Not too resistant.
        OR(2)
            // This Beholder hasn't noticed yet...
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            // or not too resistant.
            CheckStatLT(SixthNearestEnemyOf(),50,RESISTMAGIC)
        // Viconia is pretty resistant.  Beholders would know a Drow when they see one.
        !Name("Viconia",SixthNearestEnemyOf())
        // Only use on worthwile targets.
        CheckStatGT(SixthNearest(),7,LEVEL)
        // Not immortal
        !HasItem("MINHP1",SixthNearestEnemyOf())
        !HasItem("IMOENHP1",SixthNearestEnemyOf())
        // Target.
        See(SixthNearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
    // Fifth Nearest.
    IF
        !GlobalTimerNotExpired("xyxSpellCast","LOCALS")
        HaveSpell(WIZARD_POWER_WORD_KILL)
        !RandomNum(6,5)
        HPLT(FifthNearestEnemyOf(),61)
        OR(3)
            !HasBounceEffects(FifthNearestEnemyOf())
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            HasItemEquiped("WA2SHIEL",FifthNearestEnemyOf())
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !HasItemEquiped("CLCK26",FifthNearestEnemyOf())
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            !StateCheck(FifthNearestEnemyOf(),SCRIPTINGSTATE2) // Death Ward
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !Race(FifthNearestEnemyOf(),PLANATAR)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !Race(FifthNearestEnemyOf(),DARKPLANATAR)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !Race(FifthNearestEnemyOf(),SOLAR)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !Race(FifthNearestEnemyOf(),ANTISOLAR)
        !StateCheck(FifthNearestEnemyOf(),STATE_OUT_OF_ACTION)
        !StateCheck(FifthNearestEnemyOf(),STATE_IMPROVEDINVISIBILITY)
        CheckStat(FifthNearestEnemyOf(),0,SANCTUARY)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            CheckStatLT(FifthNearestEnemyOf(),50,RESISTMAGIC)
        !Name("Viconia",FifthNearestEnemyOf())
        CheckStatGT(FifthNearest(),7,LEVEL)
        !HasItem("MINHP1",FifthNearestEnemyOf())
        !HasItem("IMOENHP1",FifthNearestEnemyOf())
        See(FifthNearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
    // Fourth Nearest.
    IF
        !GlobalTimerNotExpired("xyxSpellCast","LOCALS")
        HaveSpell(WIZARD_POWER_WORD_KILL)
        !RandomNum(6,4)
        HPLT(FourthNearestEnemyOf(),61)
        OR(3)
            !HasBounceEffects(FourthNearestEnemyOf())
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            HasItemEquiped("WA2SHIEL",FourthNearestEnemyOf())
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !HasItemEquiped("CLCK26",FourthNearestEnemyOf())
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            !StateCheck(FourthNearestEnemyOf(),SCRIPTINGSTATE2) // Death Ward
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !Race(FourthNearestEnemyOf(),PLANATAR)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !Race(FourthNearestEnemyOf(),DARKPLANATAR)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !Race(FourthNearestEnemyOf(),SOLAR)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !Race(FourthNearestEnemyOf(),ANTISOLAR)
        !StateCheck(FourthNearestEnemyOf(),STATE_OUT_OF_ACTION)
        !StateCheck(FourthNearestEnemyOf(),STATE_IMPROVEDINVISIBILITY)
        CheckStat(FourthNearestEnemyOf(),0,SANCTUARY)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            CheckStatLT(FourthNearestEnemyOf(),50,RESISTMAGIC)
        !Name("Viconia",FourthNearestEnemyOf())
        CheckStatGT(FourthNearest(),7,LEVEL)
        !HasItem("MINHP1",FourthNearestEnemyOf())
        !HasItem("IMOENHP1",FourthNearestEnemyOf())
        See(FourthNearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
    // Third Nearest.
    IF
        !GlobalTimerNotExpired("xyxSpellCast","LOCALS")
        HaveSpell(WIZARD_POWER_WORD_KILL)
        !RandomNum(6,3)
        HPLT(ThirdNearestEnemyOf(),61)
        OR(3)
            !HasBounceEffects(ThirdNearestEnemyOf())
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            HasItemEquiped("WA2SHIEL",ThirdNearestEnemyOf())
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !HasItemEquiped("CLCK26",ThirdNearestEnemyOf())
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            !StateCheck(ThirdNearestEnemyOf(),SCRIPTINGSTATE2) // Death Ward
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !Race(ThirdNearestEnemyOf(),PLANATAR)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !Race(ThirdNearestEnemyOf(),DARKPLANATAR)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !Race(ThirdNearestEnemyOf(),SOLAR)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !Race(ThirdNearestEnemyOf(),ANTISOLAR)
        !StateCheck(ThirdNearestEnemyOf(),STATE_OUT_OF_ACTION)
        !StateCheck(ThirdNearestEnemyOf(),STATE_IMPROVEDINVISIBILITY)
        CheckStat(ThirdNearestEnemyOf(),0,SANCTUARY)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            CheckStatLT(ThirdNearestEnemyOf(),50,RESISTMAGIC)
        !Name("Viconia",ThirdNearestEnemyOf())
        CheckStatGT(ThirdNearest(),7,LEVEL)
        !HasItem("MINHP1",ThirdNearestEnemyOf())
        !HasItem("IMOENHP1",ThirdNearestEnemyOf())
        See(ThirdNearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
    // Second Nearest.
    IF
        !GlobalTimerNotExpired("xyxSpellCast","LOCALS")
        HaveSpell(WIZARD_POWER_WORD_KILL)
        !RandomNum(6,2)
        HPLT(SecondNearestEnemyOf(),61)
        OR(3)
            !HasBounceEffects(SecondNearestEnemyOf())
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            HasItemEquiped("WA2SHIEL",SecondNearestEnemyOf())
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !HasItemEquiped("CLCK26",SecondNearestEnemyOf())
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            !StateCheck(SecondNearestEnemyOf(),SCRIPTINGSTATE2) // Death Ward
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !Race(SecondNearestEnemyOf(),PLANATAR)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !Race(SecondNearestEnemyOf(),DARKPLANATAR)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !Race(SecondNearestEnemyOf(),SOLAR)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !Race(SecondNearestEnemyOf(),ANTISOLAR)
        !StateCheck(SecondNearestEnemyOf(),STATE_OUT_OF_ACTION)
        !StateCheck(SecondNearestEnemyOf(),STATE_IMPROVEDINVISIBILITY)
        CheckStat(SecondNearestEnemyOf(),0,SANCTUARY)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            CheckStatLT(SecondNearestEnemyOf(),50,RESISTMAGIC)
        !Name("Viconia",SecondNearestEnemyOf())
        CheckStatGT(SecondNearest(),7,LEVEL)
        !HasItem("MINHP1",SecondNearestEnemyOf())
        !HasItem("IMOENHP1",SecondNearestEnemyOf())
        See(SecondNearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
    // Nearest.
    IF
        !GlobalTimerNotExpired("xyxSpellCast","LOCALS")
        HaveSpell(WIZARD_POWER_WORD_KILL)
        !RandomNum(6,1)
        HPLT(NearestEnemyOf(),61)
        OR(3)
            !HasBounceEffects(NearestEnemyOf())
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            HasItemEquiped("WA2SHIEL",NearestEnemyOf())
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !HasItemEquiped("CLCK26",NearestEnemyOf())
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            !StateCheck(NearestEnemyOf(),SCRIPTINGSTATE2) // Death Ward
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !Race(NearestEnemyOf(),PLANATAR)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !Race(NearestEnemyOf(),DARKPLANATAR)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !Race(NearestEnemyOf(),SOLAR)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !Race(NearestEnemyOf(),ANTISOLAR)
        !StateCheck(NearestEnemyOf(),STATE_OUT_OF_ACTION)
        !StateCheck(NearestEnemyOf(),STATE_IMPROVEDINVISIBILITY)
        CheckStat(NearestEnemyOf(),0,SANCTUARY)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            CheckStatLT(NearestEnemyOf(),50,RESISTMAGIC)
        !Name("Viconia",NearestEnemyOf())
        CheckStatGT(Nearest(),7,LEVEL)
        !HasItem("MINHP1",NearestEnemyOf())
        !HasItem("IMOENHP1",NearestEnemyOf())
        See(NearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
// Power Word Kill.
IF
    !GlobalTimerNotExpired("xyxSpellCast","LOCALS")
    HaveSpell(WIZARD_POWER_WORD_KILL)
    HPLT(LastSeenBy(),61)
    // Not immune to Death.
    OR(2)
        GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
        !StateCheck(LastSeenBy(),SCRIPTINGSTATE2) // Death Ward
    OR(2)
        GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
        !Race(LastSeenBy(),PLANATAR)
    OR(2)
        GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
        !Race(LastSeenBy(),DARKPLANATAR)
    OR(2)
        GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
        !Race(LastSeenBy(),SOLAR)
    OR(2)
        GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
        !Race(LastSeenBy(),ANTISOLAR)
    // Yes, the cloak bounces Power Word Kill...
    OR(2)
        GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
        !HasItemEquiped("CLCK26",LastSeenBy()) // Cloak of Mirroring
    // Not totally magic resistant.
    OR(2)
        GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
        CheckStatLT(LastSeenBy(),100,RESISTMAGIC)
    // Not bouncing.
    // This ray is potentially deadly if it bounces, so be hesitant to use it while bouncing is not yet detected.
    OR(3)
        GlobalTimerExpired("xyxDoesNotSeeBounce","LOCALS")
        RandomNum(3,1)
        // I will not be affected (if I don't take damage in the next second...).
        HPGT(Myself,60)
    OR(3)
        !HasBounceEffects(LastSeenBy())
        GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
        HasItemEquiped("WA2SHIEL",LastSeenBy())
    !StateCheck(LastSeenBy(),STATE_IMPROVEDINVISIBILITY)
    CheckStat(LastSeenBy(),0,SANCTUARY)
    CheckStatGT(LastSeenBy(),7,LEVEL)
    !HasItem("MINHP1",LastSeenBy())
    !HasItem("IMOENHP1",LastSeenBy())
THEN
    RESPONSE #100
        SetGlobalTimer("xyxSpellCast","LOCALS",6)
        Spell(LastSeenBy(),WIZARD_POWER_WORD_KILL)
    // Caution is advised...
    RESPONSE #100
        Continue()
END
//==============================================================================
// *               BAF Name: BEHELD01.BAF
// *           Segment Name: 17 3 Targeting - Spells - Non-Damage.baf
// Find a target without Spell Turning or Magic Resistance.
// (The cut-off resets the LastSeenBy.)
    // Sixth Nearest.
    IF
        !GlobalTimerNotExpired("xyxSpellCast","LOCALS")
        // Randomize a bit.
        !RandomNum(6,6)
        // Not bouncing ALL spells.
        OR(3)
            // The target isn't bouncing spells...
            !HasBounceEffects(SixthNearestEnemyOf())
            // or this Beholder hasn't caught on yet...
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            // or the target has the Shield of Balduran.
            HasItemEquiped("WA2SHIEL",SixthNearestEnemyOf())
        // Not already out of action.
        !StateCheck(SixthNearestEnemyOf(),STATE_OUT_OF_ACTION)
        // Not Improved Invisible or in Sanctuary.
        !StateCheck(SixthNearestEnemyOf(),STATE_IMPROVEDINVISIBILITY)
        CheckStat(SixthNearestEnemyOf(),0,SANCTUARY)
        // Not too resistant.
        OR(2)
            // This Beholder hasn't noticed yet...
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            // or not too resistant.
            CheckStatLT(SixthNearestEnemyOf(),50,RESISTMAGIC)
        // Viconia is pretty resistant.  Beholders would know a Drow when they see one.
        !Name("Viconia",SixthNearestEnemyOf())
        // Not immortal.
        !HasItem("MINHP1",SixthNearestEnemyOf())
        !HasItem("IMOENHP1",SixthNearestEnemyOf())
        // Target.
        See(SixthNearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
    // Fifth Nearest.
    IF
        !GlobalTimerNotExpired("xyxSpellCast","LOCALS")
        !RandomNum(6,5)
        OR(3)
            !HasBounceEffects(FifthNearestEnemyOf())
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            HasItemEquiped("WA2SHIEL",FifthNearestEnemyOf())
        !StateCheck(FifthNearestEnemyOf(),STATE_OUT_OF_ACTION)
        !StateCheck(FifthNearestEnemyOf(),STATE_IMPROVEDINVISIBILITY)
        CheckStat(FifthNearestEnemyOf(),0,SANCTUARY)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            CheckStatLT(FifthNearestEnemyOf(),50,RESISTMAGIC)
        !Name("Viconia",FifthNearestEnemyOf())
        !HasItem("MINHP1",FifthNearestEnemyOf())
        !HasItem("IMOENHP1",FifthNearestEnemyOf())
        See(FifthNearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
    // Fourth Nearest.
    IF
        !GlobalTimerNotExpired("xyxSpellCast","LOCALS")
        !RandomNum(6,4)
        OR(3)
            !HasBounceEffects(FourthNearestEnemyOf())
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            HasItemEquiped("WA2SHIEL",FourthNearestEnemyOf())
        !StateCheck(FourthNearestEnemyOf(),STATE_OUT_OF_ACTION)
        !StateCheck(FourthNearestEnemyOf(),STATE_IMPROVEDINVISIBILITY)
        CheckStat(FourthNearestEnemyOf(),0,SANCTUARY)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            CheckStatLT(FourthNearestEnemyOf(),50,RESISTMAGIC)
        !Name("Viconia",FourthNearestEnemyOf())
        !HasItem("MINHP1",FourthNearestEnemyOf())
        !HasItem("IMOENHP1",FourthNearestEnemyOf())
        See(FourthNearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
    // Third Nearest.
    IF
        !GlobalTimerNotExpired("xyxSpellCast","LOCALS")
        !RandomNum(6,3)
        OR(3)
            !HasBounceEffects(ThirdNearestEnemyOf())
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            HasItemEquiped("WA2SHIEL",ThirdNearestEnemyOf())
        !StateCheck(ThirdNearestEnemyOf(),STATE_OUT_OF_ACTION)
        !StateCheck(ThirdNearestEnemyOf(),STATE_IMPROVEDINVISIBILITY)
        CheckStat(ThirdNearestEnemyOf(),0,SANCTUARY)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            CheckStatLT(ThirdNearestEnemyOf(),50,RESISTMAGIC)
        !Name("Viconia",ThirdNearestEnemyOf())
        !HasItem("MINHP1",ThirdNearestEnemyOf())
        !HasItem("IMOENHP1",ThirdNearestEnemyOf())
        See(ThirdNearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
    // Second Nearest.
    IF
        !GlobalTimerNotExpired("xyxSpellCast","LOCALS")
        !RandomNum(6,2)
        OR(3)
            !HasBounceEffects(SecondNearestEnemyOf())
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            HasItemEquiped("WA2SHIEL",SecondNearestEnemyOf())
        !StateCheck(SecondNearestEnemyOf(),STATE_OUT_OF_ACTION)
        !StateCheck(SecondNearestEnemyOf(),STATE_IMPROVEDINVISIBILITY)
        CheckStat(SecondNearestEnemyOf(),0,SANCTUARY)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            CheckStatLT(SecondNearestEnemyOf(),50,RESISTMAGIC)
        !Name("Viconia",SecondNearestEnemyOf())
        !HasItem("MINHP1",SecondNearestEnemyOf())
        !HasItem("IMOENHP1",SecondNearestEnemyOf())
        See(SecondNearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
    // Nearest.
    IF
        !GlobalTimerNotExpired("xyxSpellCast","LOCALS")
        !RandomNum(6,1)
        OR(3)
            !HasBounceEffects(NearestEnemyOf())
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            HasItemEquiped("WA2SHIEL",NearestEnemyOf())
        !StateCheck(NearestEnemyOf(),STATE_OUT_OF_ACTION)
        !StateCheck(NearestEnemyOf(),STATE_IMPROVEDINVISIBILITY)
        CheckStat(NearestEnemyOf(),0,SANCTUARY)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            CheckStatLT(NearestEnemyOf(),50,RESISTMAGIC)
        !Name("Viconia",NearestEnemyOf())
        !HasItem("MINHP1",NearestEnemyOf())
        !HasItem("IMOENHP1",NearestEnemyOf())
        See(NearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
//==============================================================================
// *               BAF Name: BEHELD01.BAF
// *           Segment Name: 18 4 Spell - 1 Maze.baf
    // Maze.
    IF
        !GlobalTimerNotExpired("xyxSpellCast","LOCALS")
        HaveSpell(WIZARD_MAZE)
        // Not immune to Maze.
        // Rage is pretty obvious.
        !StateCheck(LastSeenBy(),STATE_BERSERK) // Minsc Berserk
        !StateCheck(LastSeenBy(),SCRIPTINGSTATE3) // Barbarian Rage
        !Kit(LastSeenBy(Myself),BERSERKER)
        // Not bouncing.
        // This spell is disabling if it bounces, so be hesitant to use it while bouncing is not yet detected.
        OR(2)
            GlobalTimerExpired("xyxDoesNotSeeBounce","LOCALS")
            RandomNum(2,1)
        // Double-check.
        OR(3)
            !HasBounceEffects(LastSeenBy())
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            HasItemEquiped("WA2SHIEL",LastSeenBy())
        !StateCheck(LastSeenBy(),STATE_OUT_OF_ACTION)
        // Legal target.
        !StateCheck(LastSeenBy(),STATE_IMPROVEDINVISIBILITY)
        CheckStat(LastSeenBy(),0,SANCTUARY)
        // Not immortal.
        !HasItem("MINHP1",LastSeenBy())
        !HasItem("IMOENHP1",LastSeenBy())
        // Don't waste this on low-level opponents.
        CheckStatGT(LastSeenBy(),7,LEVEL)
        // Avoid Mazing the last party member sometimes.
        OR(2)
            // There are still others...
            NumInPartyAliveGT(1)
            // or I'm feeling nasty.
            RandomNum(4,1)
    THEN
        RESPONSE #100
            SetGlobalTimer("xyxSpellCast","LOCALS",6)
            Spell(LastSeenBy(),WIZARD_MAZE)
        RESPONSE #100
            Continue()
    END
// *               BAF Name: BEHELD01.BAF
// *           Segment Name: 19 4 Spell - 4 True Sight.baf
    // True Sight.
    IF
        !GlobalTimerNotExpired("xyxSpellCast","LOCALS")
        HaveSpell(WIZARD_TRUE_SIGHT)
        // True Sight from Chain Contingency has worn off.
        GlobalTimerExpired("xyxTrueSight","LOCALS")
        OR(11)
            // Someone is using illusions...
            StateCheck(NearestEnemyOf(),STATE_ILLUSIONS) // Invisibility, Improved Invisibility, Mirror Image or Blur.
            StateCheck(SecondNearestEnemyOf(),STATE_ILLUSIONS)
            StateCheck(ThirdNearestEnemyOf(),STATE_ILLUSIONS)
            StateCheck(FourthNearestEnemyOf(),STATE_ILLUSIONS)
            StateCheck(FifthNearestEnemyOf(),STATE_ILLUSIONS)
            StateCheck(SixthNearestEnemyOf(),STATE_ILLUSIONS)
            StateCheck(SeventhNearestEnemyOf(),STATE_ILLUSIONS)
            StateCheck(EighthNearestEnemyOf(),STATE_ILLUSIONS)
            StateCheck(NinthNearestEnemyOf(),STATE_ILLUSIONS)
            StateCheck(TenthNearestEnemyOf(),STATE_ILLUSIONS)
            // or there's an illusory creature around.
            NumCreatureGT([GOODCUTOFF.0.0.0.0.ILLUSIONARY],0)
    THEN
        RESPONSE #100
            SetGlobalTimer("xyxSpellCast","LOCALS",6)
            Spell(Myself,WIZARD_TRUE_SIGHT)
        RESPONSE #100
            Continue()
    END
// *               BAF Name: BEHELD01.BAF
// *           Segment Name: 20 4 Spell - 4 Vocalize.baf
    // Vocalize.
    IF
        !GlobalTimerNotExpired("xyxSpellCast","LOCALS")
        HaveSpell(WIZARD_VOCALIZE)
        StateCheck(Myself,STATE_SILENCED)
        // At least one other spell left to cast.
        OR(5)
            HaveSpell(WIZARD_TRUE_SIGHT)
            HaveSpell(WIZARD_SPELL_TURNING)
            HaveSpell(WIZARD_MAZE)
            HaveSpell(WIZARD_IMPRISONMENT)
            HaveSpell(WIZARD_POWER_WORD_KILL)
    THEN
        RESPONSE #100
            SetGlobalTimer("xyxSpellCast","LOCALS",6)
            Spell(Myself,WIZARD_VOCALIZE)
        RESPONSE #100
            Continue()
    END
// *               BAF Name: BEHELD01.BAF
// *           Segment Name: 21 4 Ray - 2 Anti-Magic - Spell Failure.baf
// Target spellcasters for Anti-Magic Ray.
    // Second Nearest Cleric.
    IF
        !GlobalTimerNotExpired("xyxUsedAntiMagicRay","LOCALS")
        // Randomize a bit.
        !RandomNum(6,6)
        // The target can still cast spells.
        CheckStatLT(SecondNearestEnemyOfType([0.0.0.CLERIC_ALL]),100,SPELLFAILUREPRIEST)
        !StateCheck(SecondNearestEnemyOfType([0.0.0.CLERIC_ALL]),STATE_OUT_OF_ACTION)
        See(SecondNearestEnemyOfType([0.0.0.CLERIC_ALL]))
        False()
    THEN
        RESPONSE #100
    END
    // Nearest Cleric.
    IF
        !GlobalTimerNotExpired("xyxUsedAntiMagicRay","LOCALS")
        !RandomNum(6,5)
        CheckStatLT(NearestEnemyOfType([0.0.0.CLERIC_ALL]),100,SPELLFAILUREPRIEST)
        !StateCheck(NearestEnemyOfType([0.0.0.CLERIC_ALL]),STATE_OUT_OF_ACTION)
        See(NearestEnemyOfType([0.0.0.CLERIC_ALL]))
        False()
    THEN
        RESPONSE #100
    END
    // Nearest Bard.
    IF
        !GlobalTimerNotExpired("xyxUsedAntiMagicRay","LOCALS")
        !RandomNum(6,4)
        CheckStatLT(NearestEnemyOfType([0.0.0.BARD]),100,SPELLFAILUREMAGE)
        !StateCheck(NearestEnemyOfType([0.0.0.BARD]),STATE_OUT_OF_ACTION)
        See(NearestEnemyOfType([0.0.0.BARD]))
        False()
    THEN
        RESPONSE #100
    END
    // Nearest Druid.
    IF
        !GlobalTimerNotExpired("xyxUsedAntiMagicRay","LOCALS")
        !RandomNum(6,3)
        CheckStatLT(NearestEnemyOfType([0.0.0.DRUID_ALL]),100,SPELLFAILUREPRIEST)
        !StateCheck(NearestEnemyOfType([0.0.0.DRUID_ALL]),STATE_OUT_OF_ACTION)
        See(NearestEnemyOfType([0.0.0.DRUID_ALL]))
        False()
    THEN
        RESPONSE #100
    END
    // Second Nearest Mage.
    IF
        !GlobalTimerNotExpired("xyxUsedAntiMagicRay","LOCALS")
        !RandomNum(6,2)
        CheckStatLT(SecondNearestEnemyOfType([0.0.0.MAGE_ALL]),100,SPELLFAILUREMAGE)
        !StateCheck(SecondNearestEnemyOfType([0.0.0.MAGE_ALL]),STATE_OUT_OF_ACTION)
        See(SecondNearestEnemyOfType([0.0.0.MAGE_ALL]))
        False()
    THEN
        RESPONSE #100
    END
    // Nearest Mage.
    IF
        !GlobalTimerNotExpired("xyxUsedAntiMagicRay","LOCALS")
        !RandomNum(6,1)
        CheckStatLT(NearestEnemyOfType([0.0.0.MAGE_ALL]),100,SPELLFAILUREMAGE)
        !StateCheck(NearestEnemyOfType([0.0.0.MAGE_ALL]),STATE_OUT_OF_ACTION)
        See(NearestEnemyOfType([0.0.0.MAGE_ALL]))
        False()
    THEN
        RESPONSE #100
    END
// Use Anti-Magic Ray to force spell failure in spellcasters.
IF
    !GlobalTimerNotExpired("xyxUsedAntiMagicRay","LOCALS")
    !CheckStat(LastSeenBy(),100,SPELLFAILUREMAGE)
    !CheckStat(LastSeenBy(),100,SPELLFAILUREPRIEST)
    OR(6)
        Class(LastSeenBy(),MAGE_ALL)
        Class(LastSeenBy(),DRUID_ALL)
        Class(LastSeenBy(),BARD)
        Class(LastSeenBy(),CLERIC_ALL)
        Name(LastSeenBy(),"Planatar") // Good Planetar
        Name(LastSeenBy(),"Darkplanatar") // Evil Planetar
    // Don't use too much on downed foes.
    OR(2)
        !StateCheck(LastSeenBy(),STATE_OUT_OF_ACTION)
        RandomNum(4,1)
THEN
    RESPONSE #100
        SetGlobalTimer("xyxUsedAntiMagicRay","LOCALS",6)
        ForceSpell(LastSeenBy(),BEHOLDER_ANTIMAGIC_RAY)
        // Wait(XXX)
    RESPONSE #100
        Continue()
END
//==============================================================================
// *               BAF Name: BEHELD01.BAF
// *           Segment Name: 22 4 Ray - 2 Anti-Magic - Dispel.baf
// Target enemies with dispellable enchantments.
    // Sixth Nearest.
    IF
        !GlobalTimerNotExpired("xyxUsedAntiMagicRay","LOCALS")
        // Randomize a bit.
        !RandomNum(6,6)
        // The target has some juicy enchantments.
        OR(4)
            StateCheck(SixthNearestEnemyOf(),STATE_ENCHANTED) // Mirror Image, Blur, Chant, Improved Invisibility, Bless, Haste, Invisibility
            CheckStat(SixthNearestEnemyOf(),1,MINORGLOBE)
            CheckStat(SixthNearestEnemyOf(),1,SCRIPTINGSTATE2)
            CheckStatGT(SixthNearestEnemyOf(),0,STONESKINS)
        !StateCheck(SixthNearestEnemyOf(),STATE_OUT_OF_ACTION)
        !HasItem("MINHP1",SixthNearestEnemyOf())
        !HasItem("IMOENHP1",SixthNearestEnemyOf())
        See(SixthNearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
    // Fifth Nearest.
    IF
        !GlobalTimerNotExpired("xyxUsedAntiMagicRay","LOCALS")
        !RandomNum(6,5)
        OR(4)
            StateCheck(FifthNearestEnemyOf(),STATE_ENCHANTED) // Mirror Image, Blur, Chant, Improved Invisibility, Bless, Haste, Invisibility
            CheckStat(FifthNearestEnemyOf(),1,MINORGLOBE)
            CheckStat(FifthNearestEnemyOf(),1,SCRIPTINGSTATE2)
            CheckStatGT(FifthNearestEnemyOf(),0,STONESKINS)
        !StateCheck(FifthNearestEnemyOf(),STATE_OUT_OF_ACTION)
        !HasItem("MINHP1",FifthNearestEnemyOf())
        !HasItem("IMOENHP1",FifthNearestEnemyOf())
        See(FifthNearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
    // FourthNearest.
    IF
        !GlobalTimerNotExpired("xyxUsedAntiMagicRay","LOCALS")
        !RandomNum(6,4)
        OR(4)
            StateCheck(FourthNearestEnemyOf(),STATE_ENCHANTED) // Mirror Image, Blur, Chant, Improved Invisibility, Bless, Haste, Invisibility
            CheckStat(FourthNearestEnemyOf(),1,MINORGLOBE)
            CheckStat(FourthNearestEnemyOf(),1,SCRIPTINGSTATE2)
            CheckStatGT(FourthNearestEnemyOf(),0,STONESKINS)
        !StateCheck(FourthNearestEnemyOf(),STATE_OUT_OF_ACTION)
        !HasItem("MINHP1",FourthNearestEnemyOf())
        !HasItem("IMOENHP1",FourthNearestEnemyOf())
        See(FourthNearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
    // ThirdNearest.
    IF
        !GlobalTimerNotExpired("xyxUsedAntiMagicRay","LOCALS")
        !RandomNum(6,3)
        OR(4)
            StateCheck(ThirdNearestEnemyOf(),STATE_ENCHANTED) // Mirror Image, Blur, Chant, Improved Invisibility, Bless, Haste, Invisibility
            CheckStat(ThirdNearestEnemyOf(),1,MINORGLOBE)
            CheckStat(ThirdNearestEnemyOf(),1,SCRIPTINGSTATE2)
            CheckStatGT(ThirdNearestEnemyOf(),0,STONESKINS)
        !StateCheck(ThirdNearestEnemyOf(),STATE_OUT_OF_ACTION)
        !HasItem("MINHP1",ThirdNearestEnemyOf())
        !HasItem("IMOENHP1",ThirdNearestEnemyOf())
        See(ThirdNearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
    // SecondNearest.
    IF
        !GlobalTimerNotExpired("xyxUsedAntiMagicRay","LOCALS")
        !RandomNum(6,2)
        OR(4)
            StateCheck(SecondNearestEnemyOf(),STATE_ENCHANTED) // Mirror Image, Blur, Chant, Improved Invisibility, Bless, Haste, Invisibility
            CheckStat(SecondNearestEnemyOf(),1,MINORGLOBE)
            CheckStat(SecondNearestEnemyOf(),1,SCRIPTINGSTATE2)
            CheckStatGT(SecondNearestEnemyOf(),0,STONESKINS)
        !StateCheck(SecondNearestEnemyOf(),STATE_OUT_OF_ACTION)
        !HasItem("MINHP1",SecondNearestEnemyOf())
        !HasItem("IMOENHP1",SecondNearestEnemyOf())
        See(SecondNearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
    // Nearest.
    IF
        !GlobalTimerNotExpired("xyxUsedAntiMagicRay","LOCALS")
        !RandomNum(6,1)
        OR(4)
            StateCheck(NearestEnemyOf(),STATE_ENCHANTED) // Mirror Image, Blur, Chant, Improved Invisibility, Bless, Haste, Invisibility
            CheckStat(NearestEnemyOf(),1,MINORGLOBE)
            CheckStat(NearestEnemyOf(),1,SCRIPTINGSTATE2)
            CheckStatGT(NearestEnemyOf(),0,STONESKINS)
        !StateCheck(NearestEnemyOf(),STATE_OUT_OF_ACTION)
        !HasItem("MINHP1",NearestEnemyOf())
        !HasItem("IMOENHP1",NearestEnemyOf())
        See(NearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
// Use Anti-Magic Ray to dispel enchantments.
// (Does not remove bounce spells...)
IF
    !GlobalTimerNotExpired("xyxUsedAntiMagicRay","LOCALS")
    // The target has some juicy enchantments.
    OR(4)
        StateCheck(LastSeenBy(),STATE_ENCHANTED) // Mirror Image, Blur, Chant, Improved Invisibility, Bless, Haste, Invisibility
        CheckStat(LastSeenBy(),1,MINORGLOBE)
        CheckStat(LastSeenBy(),1,SCRIPTINGSTATE2)
        CheckStatGT(LastSeenBy(),0,STONESKINS)
    // This ray does not bounce.
    !HasItem("MINHP1",LastSeenBy())
    !HasItem("IMOENHP1",LastSeenBy())
THEN
    RESPONSE #100
        SetGlobalTimer("xyxUsedAntiMagicRay","LOCALS",6)
        ForceSpell(LastSeenBy(),BEHOLDER_ANTIMAGIC_RAY)
        // Wait(XXX)
    RESPONSE #100
        Continue()
END
//==============================================================================
// *               BAF Name: BEHELD01.BAF
// *           Segment Name: 23 3 Targeting - Rays - Disabling.baf
// Find a target without Spell Turning, Magic Resistance or the Shield of Balduran.
// (The cut-off resets the LastSeenBy.)
// (Improved Invisibility is no problem for ForceSpell.)
    // Sixth Nearest.
    IF
        // Randomize a bit.
        !RandomNum(6,6)
        // Not bouncing ALL spells.
        OR(3)
            // The target isn't bouncing spells...
            !HasBounceEffects(SixthNearestEnemyOf())
            // or this Beholder hasn't caught on yet...
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            // or the target has the Cloak of Mirroring.
            HasItemEquiped("CLCK26",SixthNearestEnemyOf())
        OR(2)
            // This Beholder hasn't caught on yet...
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            // or the target doesn't have the Shield of Balduran.
            !HasItemEquiped("WA2SHIEL",SixthNearestEnemyOf())
        // Not already out of action.
        !StateCheck(SixthNearestEnemyOf(),STATE_OUT_OF_ACTION)
        // Not in Sanctuary.
        CheckStat(SixthNearestEnemyOf(),0,SANCTUARY)
        // Not too resistant.
        OR(2)
            // This Beholder hasn't noticed yet...
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            // or not too resistant.
            CheckStatLT(SixthNearestEnemyOf(),50,RESISTMAGIC)
        // Viconia is pretty resistant.  Beholders would know a Drow when they see one.
        !Name("Viconia",SixthNearestEnemyOf())
        // Not immortal.
        !HasItem("MINHP1",SixthNearestEnemyOf())
        !HasItem("IMOENHP1",SixthNearestEnemyOf())
        // Target.
        See(SixthNearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
    // Fifth Nearest.
    IF
        !RandomNum(6,5)
        OR(3)
            !HasBounceEffects(FifthNearestEnemyOf())
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            HasItemEquiped("CLCK26",FifthNearestEnemyOf())
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !HasItemEquiped("WA2SHIEL",FifthNearestEnemyOf())
        !StateCheck(FifthNearestEnemyOf(),STATE_OUT_OF_ACTION)
        CheckStat(FifthNearestEnemyOf(),0,SANCTUARY)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            CheckStatLT(FifthNearestEnemyOf(),50,RESISTMAGIC)
        !Name("Viconia",FifthNearestEnemyOf())
        !HasItem("MINHP1",FifthNearestEnemyOf())
        !HasItem("IMOENHP1",FifthNearestEnemyOf())
        See(FifthNearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
    // Fourth Nearest.
    IF
        !RandomNum(6,4)
        OR(3)
            !HasBounceEffects(FourthNearestEnemyOf())
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            HasItemEquiped("CLCK26",FourthNearestEnemyOf())
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !HasItemEquiped("WA2SHIEL",FourthNearestEnemyOf())
        !StateCheck(FourthNearestEnemyOf(),STATE_OUT_OF_ACTION)
        CheckStat(FourthNearestEnemyOf(),0,SANCTUARY)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            CheckStatLT(FourthNearestEnemyOf(),50,RESISTMAGIC)
        !Name("Viconia",FourthNearestEnemyOf())
        !HasItem("MINHP1",FourthNearestEnemyOf())
        !HasItem("IMOENHP1",FourthNearestEnemyOf())
        See(FourthNearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
    // Third Nearest.
    IF
        !RandomNum(6,3)
        OR(3)
            !HasBounceEffects(ThirdNearestEnemyOf())
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            HasItemEquiped("CLCK26",ThirdNearestEnemyOf())
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !HasItemEquiped("WA2SHIEL",ThirdNearestEnemyOf())
        !StateCheck(ThirdNearestEnemyOf(),STATE_OUT_OF_ACTION)
        CheckStat(ThirdNearestEnemyOf(),0,SANCTUARY)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            CheckStatLT(ThirdNearestEnemyOf(),50,RESISTMAGIC)
        !Name("Viconia",ThirdNearestEnemyOf())
        !HasItem("MINHP1",ThirdNearestEnemyOf())
        !HasItem("IMOENHP1",ThirdNearestEnemyOf())
        See(ThirdNearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
    // Second Nearest.
    IF
        !RandomNum(6,2)
        OR(3)
            !HasBounceEffects(SecondNearestEnemyOf())
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            HasItemEquiped("CLCK26",SecondNearestEnemyOf())
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !HasItemEquiped("WA2SHIEL",SecondNearestEnemyOf())
        !StateCheck(SecondNearestEnemyOf(),STATE_OUT_OF_ACTION)
        CheckStat(SecondNearestEnemyOf(),0,SANCTUARY)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            CheckStatLT(SecondNearestEnemyOf(),50,RESISTMAGIC)
        !Name("Viconia",SecondNearestEnemyOf())
        !HasItem("MINHP1",SecondNearestEnemyOf())
        !HasItem("IMOENHP1",SecondNearestEnemyOf())
        See(SecondNearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
    // Nearest.
    IF
        !RandomNum(6,1)
        OR(3)
            !HasBounceEffects(NearestEnemyOf())
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            HasItemEquiped("CLCK26",NearestEnemyOf())
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !HasItemEquiped("WA2SHIEL",NearestEnemyOf())
        !StateCheck(NearestEnemyOf(),STATE_OUT_OF_ACTION)
        CheckStat(NearestEnemyOf(),0,SANCTUARY)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            CheckStatLT(NearestEnemyOf(),50,RESISTMAGIC)
        !Name("Viconia",NearestEnemyOf())
        !HasItem("MINHP1",NearestEnemyOf())
        !HasItem("IMOENHP1",NearestEnemyOf())
        See(NearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
//==============================================================================
// *               BAF Name: BEHELD01.BAF
// *           Segment Name: 24 4 Ray - 2 Slow - IF.baf
    // Slow Ray.
    IF
        HPGT(Myself,35)
        !GlobalTimerNotExpired("xyxUsedSlowRay","LOCALS")
        !StateCheck(LastSeenBy(),STATE_SLOWED)
        // Only slow fighting types.
        OR(6)
            Class(LastSeenBy(),RANGER_ALL)
            Class(LastSeenBy(),FIGHTER_ALL)
            Class(LastSeenBy(),PALADIN)
            Class(LastSeenBy(),THIEF)
            Class(LastSeenBy(),BARBARIAN)
            Gender(LastSeenBy(),SUMMONED)
        // Saves not too good.
        CheckStatGT(LastSeenBy(),-3,SAVEVSSPELL)
        // Not immune to Slow.
        !Class(LastSeenBy(),MONK)
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            !HasItemEquiped("FREERING",LastSeenBy()) // Ring of Free Action
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            !HasItemEquiped("ROSSRING",LastSeenBy()) // Ring of Free Action
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            !HasItemEquiped("RING09",LastSeenBy()) // Ring of Free Action
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            !HasItemEquiped("NPSW01",LastSeenBy()) // Sword of Avoreen
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeBounce","LOCALS")
            !HasItemEquiped("WA2SHIEL",LastSeenBy()) // Shield of Balduran
        // Don't use too much on downed foes.
        OR(2)
            !StateCheck(LastSeenBy(),STATE_OUT_OF_ACTION)
            RandomNum(4,1)
        // Not totally magic resistant.
        OR(2)
            GlobalTimerNotExpired("xyxDoesNotSeeImmunity","LOCALS")
            CheckStatLT(LastSeenBy(),100,RESISTMAGIC)
        // Not immortal.
        !HasItem("MINHP1",LastSeenBy())
        !HasItem("IMOENHP1",LastSeenBy())
// *               BAF Name: BEHELD01.BAF
// *           Segment Name: 25 4 Ray - 2 Slow - THEN.baf
    THEN
        RESPONSE #100
            SetGlobalTimer("xyxUsedSlowRay","LOCALS",6)
            ForceSpell(LastSeenBy(),BEHOLDER_SLOW)
            Wait(2)
        RESPONSE #100
            Continue()
    END
// *               BAF Name: BEHELD01.BAF
// *           Segment Name: 26 4 Spell - 4 Spell Turning.baf
    // Spell Turning.
    IF
        !GlobalTimerNotExpired("xyxSpellCast","LOCALS")
        HaveSpell(WIZARD_SPELL_TURNING)
        !HasBounceEffects(Myself)
        // There's a nasty spellcaster.
        OR(4)
            Exists([GOODCUTOFF.0.0.BARD])
            Exists([GOODCUTOFF.0.0.DRUID_ALL])
            Exists([GOODCUTOFF.0.0.CLERIC_ALL])
            Exists([GOODCUTOFF.0.0.MAGE_ALL])
    THEN
        RESPONSE #100
            SetGlobalTimer("xyxSpellCast","LOCALS",6)
            Spell(Myself,WIZARD_SPELL_TURNING)
        RESPONSE #100
            Continue()
    END
// *               BAF Name: BEHELD01.BAF
// *           Segment Name: 27 5 Attack - 1 Melee - 1 Enemies.baf
// Target a vulnerable foe.
    // Sixth nearest.
    IF
        // Randomize a bit.
        !RandomNum(6,6)
        // Beholder teeth do Piercing damage.
        CheckStatLT(SixthNearestEnemyOf(),100,RESISTPIERCING)
        // Don't go after immortals (such as pre-ToB Wizard Eyes).
        !HasItem("MINHP1",SixthNearestEnemyOf())
        !HasItem("IMOENHP1",SixthNearestEnemyOf())
        See(SixthNearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
    // Fifth nearest.
    IF
        !RandomNum(6,5)
        CheckStatLT(FifthNearestEnemyOf(),100,RESISTPIERCING)
        !HasItem("MINHP1",FifthNearestEnemyOf())
        !HasItem("IMOENHP1",FifthNearestEnemyOf())
        See(FifthNearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
    // Fourth nearest.
    IF
        !RandomNum(6,4)
        CheckStatLT(FourthNearestEnemyOf(),100,RESISTPIERCING)
        !HasItem("MINHP1",FourthNearestEnemyOf())
        !HasItem("IMOENHP1",FourthNearestEnemyOf())
        See(FourthNearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
    // Third nearest.
    IF
        !RandomNum(6,3)
        CheckStatLT(ThirdNearestEnemyOf(),100,RESISTPIERCING)
        !HasItem("MINHP1",ThirdNearestEnemyOf())
        !HasItem("IMOENHP1",ThirdNearestEnemyOf())
        See(ThirdNearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
    // Second nearest.
    IF
        !RandomNum(6,2)
        StateCheck(SecondNearestEnemyOf(),STATE_CHARMED)
        CheckStatLT(SecondNearestEnemyOf(),100,RESISTPIERCING)
        !HasItem("MINHP1",SecondNearestEnemyOf())
        !HasItem("IMOENHP1",SecondNearestEnemyOf())
        See(SecondNearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
    // Nearest.
    IF
        !RandomNum(6,1)
        CheckStatLT(NearestEnemyOf(),100,RESISTPIERCING)
        !HasItem("MINHP1",NearestEnemyOf())
        !HasItem("IMOENHP1",NearestEnemyOf())
        See(NearestEnemyOf())
        False()
    THEN
        RESPONSE #100
    END
// Attack.
IF
    OR(2)
        Allegiance(LastSeenBy(),GOODCUTOFF)
        StateCheck(Myself,STATE_CHARMED)
    CheckStatLT(LastSeenBy(),100,RESISTPIERCING)
    !HasItem("MINHP1",LastSeenBy())
    !HasItem("IMOENHP1",LastSeenBy())
THEN
    RESPONSE #100
        Attack(LastSeenBy())
END
//==============================================================================
// *               BAF Name: BEHELD01.BAF
// *           Segment Name: 28 5 Attack - 1 Melee - 2 Charmed Creatures.baf
// Find a charmed creature (be they enemy or ally...).
    // Sixth nearest.
    IF
        // Search for charmed creatures.
        StateCheck(SixthNearest(),STATE_CHARMED)
        // Beholder teeth do Piercing damage.
        CheckStatLT(SixthNearest(),100,RESISTPIERCING)
        // Don't go after immortals (such as pre-ToB Wizard Eyes).
        !HasItem("MINHP1",SixthNearest())
        !HasItem("IMOENHP1",SixthNearest())
        See(SixthNearest())
        False()
    THEN
        RESPONSE #100
    END
    // Fifth nearest.
    IF
        StateCheck(FifthNearest(),STATE_CHARMED)
        CheckStatLT(FifthNearest(),100,RESISTPIERCING)
        !HasItem("MINHP1",FifthNearest())
        !HasItem("IMOENHP1",FifthNearest())
        See(FifthNearest())
        False()
    THEN
        RESPONSE #100
    END
    // Fourth nearest.
    IF
        StateCheck(FourthNearest(),STATE_CHARMED)
        CheckStatLT(FourthNearest(),100,RESISTPIERCING)
        !HasItem("MINHP1",FourthNearest())
        !HasItem("IMOENHP1",FourthNearest())
        See(FourthNearest())
        False()
    THEN
        RESPONSE #100
    END
    // Third nearest.
    IF
        StateCheck(ThirdNearest(),STATE_CHARMED)
        CheckStatLT(ThirdNearest(),100,RESISTPIERCING)
        !HasItem("MINHP1",ThirdNearest())
        !HasItem("IMOENHP1",ThirdNearest())
        See(ThirdNearest())
        False()
    THEN
        RESPONSE #100
    END
    // Second nearest.
    IF
        StateCheck(SecondNearest(),STATE_CHARMED)
        CheckStatLT(SecondNearest(),100,RESISTPIERCING)
        !HasItem("MINHP1",SecondNearest())
        !HasItem("IMOENHP1",SecondNearest())
        See(SecondNearest())
        False()
    THEN
        RESPONSE #100
    END
    // Nearest.
    IF
        StateCheck(Nearest(),STATE_CHARMED)
        CheckStatLT(Nearest(),100,RESISTPIERCING)
        !HasItem("MINHP1",Nearest())
        !HasItem("IMOENHP1",Nearest())
        See(Nearest())
        False()
    THEN
        RESPONSE #100
    END
// Attack.
IF
    !StateCheck(Myself,STATE_CHARMED)
    StateCheck(LastSeenBy(),STATE_CHARMED)
THEN
    RESPONSE #100
        Attack(LastSeenBy())
END
//==============================================================================
// *               BAF Name: BEHELD01.BAF
// *           Segment Name: 29 6 Background Script Block - Stand Ground.baf
// Prevent background scripts from doing something stupid...
IF
    True()
THEN
    RESPONSE #100
        NoAction()
END
//==============================================================================
