IF
  HPLT(Myself,15)
  GlobalLT("EfreetiGasForm","LOCALS",3)
  !StateCheck(Myself,STATE_DEAD)
THEN
  RESPONSE #100
    IncrementGlobal("EfreetiGasForm","LOCALS",1)
    ReallyForceSpell(Myself,EFREETI_GAS_FORM_CHANGE)
END

IF
  See(NearestEnemyOf(Myself))
  !StateCheck(Myself,STATE_INVISIBLE)
  Global("Shield","LOCALS",0)
THEN
  RESPONSE #100
    ApplySpell(Myself,WIZARD_FIRE_SHIELD_RED)
    SetGlobal("Shield","LOCALS",1)
END

IF
  See(NearestEnemyOf(Myself))
  StateCheck(Myself,STATE_INVISIBLE)
  Global("EfreetiFireball","LOCALS",0)
THEN
  RESPONSE #100
    ReallyForceSpell(NearestEnemyOf(Myself),WIZARD_FIREBALL)
    SetGlobal("EfreetiFireball","LOCALS",1)
END

IF
  See(NearestEnemyOf(Myself))
  !StateCheck(Myself,STATE_INVISIBLE)
  GlobalLT("EfreetiInvisibility","LOCALS",3)
THEN
  RESPONSE #100
    IncrementGlobal("EfreetiInvisibility","LOCALS",1)
    ForceSpell(Myself,WIZARD_INVISIBILITY)
    Wait(1)
END

IF
  HaveSpell(WIZARD_GHOST_ARMOR)
  CheckStatGT(Myself,2,ARMORCLASS)
  RandomNumLT(118,40)
THEN
  RESPONSE #100
    Spell(Myself,WIZARD_GHOST_ARMOR)
END

IF
  See(NearestEnemyOf(Myself))
  Global("arrow","LOCALS",0)
THEN
  RESPONSE #100
    ForceSpell(NearestEnemyOf(Myself),WIZARD_FLAME_ARROW)
    SetGlobal("arrow","LOCALS",1)
END

IF
  HaveSpell(WIZARD_LIGHTNING_BOLT)
  CheckStatLT(LastSeenBy(Myself),50,RESISTELECTRICITY)
  CheckStatLT(LastSeenBy(Myself),50,RESISTMAGIC)
  !HasBounceEffects(LastSeenBy(Myself))
  !Range(LastSeenBy(Myself),6)
  RandomNumLT(72,25)
THEN
  RESPONSE #100
    Spell(LastSeenBy(Myself),WIZARD_LIGHTNING_BOLT)
END

IF
  HaveSpell(WIZARD_AGANNAZAR_SCORCHER)
  CheckStatLT(LastSeenBy(Myself),50,RESISTMAGIC)
  CheckStatLT(LastSeenBy(Myself),50,RESISTMAGICFIRE)
  RandomNumLT(75,16)
THEN
  RESPONSE #100
    Spell(LastSeenBy(Myself),WIZARD_AGANNAZAR_SCORCHER)
END

IF
  HaveSpell(WIZARD_MELF_ACID_ARROW)
  CheckStatLT(LastSeenBy(Myself),50,RESISTMAGIC)
  RandomNumLT(76,16)
THEN
  RESPONSE #100
    Spell(LastSeenBy(Myself),WIZARD_MELF_ACID_ARROW)
END

IF
  HaveSpell(WIZARD_STINKING_CLOUD)
  !Range(LastSeenBy(Myself),10)
  !StateCheck(LastSeenBy(Myself),STATE_HELPLESS)
  !StateCheck(LastSeenBy(Myself),STATE_SLEEPING)
  RandomNumLT(143,32)
THEN
  RESPONSE #100
    Spell(LastSeenBy(Myself),WIZARD_STINKING_CLOUD)
END

IF
  HaveSpell(WIZARD_MAGIC_MISSILE)
  CheckStatLT(LastSeenBy(Myself),50,RESISTMAGIC)
  !HasBounceEffects(LastSeenBy(Myself))
  RandomNumLT(79,28)
THEN
  RESPONSE #100
    Spell(LastSeenBy(Myself),WIZARD_MAGIC_MISSILE)
END

IF
  See(NearestEnemyOf(Myself))
  Global("strike","LOCALS",0)
THEN
  RESPONSE #100
    ForceSpell(NearestEnemyOf(Myself),CLERIC_FLAME_STRIKE)
    SetGlobal("strike","LOCALS",1)
END

IF
  See(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    AttackReevaluate(NearestEnemyOf(Myself),15)
END

IF
  !See([GOODCUTOFF])
  Allegiance(Myself,ENEMY)
THEN
  RESPONSE #100
    MoveToObject(Player1)
END

